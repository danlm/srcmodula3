<HTML>
<HEAD>
<TITLE>SRC Modula-3: runtime/src/common/RTHeapStats.m3</TITLE>
</HEAD>
<BODY>
<A NAME="0TOP0">
<H2>runtime/src/common/RTHeapStats.m3</H2></A><HR>
<inModule>
<PRE><A HREF="../../../COPYRIGHT.html">Copyright (C) 1994, Digital Equipment Corp.</A>
</PRE><BLOCKQUOTE><EM>                                                             </EM></BLOCKQUOTE><PRE>
</PRE> The code below makes the following NASTY assumption:
      ThreadF.ProcessStacks calls its argument twice for
      each thread -- the first time for the stack, the
      second time for its registers. 

<P><PRE>UNSAFE MODULE <module><implements><A HREF="RTHeapStats.i3.html">RTHeapStats</A></implements></module>;

IMPORT <A HREF="RT0.i3.html">RT0</A>, <A HREF="#x1">RT0u</A>, <A HREF="RTCollector.i3.html">RTCollector</A>, <A HREF="RTModule.i3.html">RTModule</A>, <A HREF="RTIO.i3.html">RTIO</A>, <A HREF="RTHeapMap.i3.html">RTHeapMap</A>, <A HREF="RTHeapRep.i3.html">RTHeapRep</A>, <A HREF="RTMisc.i3.html">RTMisc</A>;
IMPORT <A HREF="RTOS.i3.html">RTOS</A>, <A HREF="RTType.i3.html">RTType</A>, <A HREF="RTTypeSRC.i3.html">RTTypeSRC</A>, <A HREF="RTProcedure.i3.html">RTProcedure</A>, <A HREF="RTProcedureSRC.i3.html">RTProcedureSRC</A>, <A HREF="#x2">RTMachine</A>;
IMPORT <A HREF="RTStack.i3.html">RTStack</A>, <A HREF="#x3">ThreadF</A>, <A HREF="../../../word/src/Word.i3.html">Word</A>, <A HREF="../../../text/src/Text.i3.html">Text</A>;
FROM <A HREF="RTIO.i3.html">RTIO</A> IMPORT PutInt, PutAddr, PutText;

TYPE
  Info = RECORD
    module    : RT0.ModulePtr;
    thread_id : INTEGER;
    location  : ADDRESS;
    ref       : ADDRESS;
    n_objects : INTEGER;
    n_bytes   : INTEGER;
  END;

TYPE
  InfoSet = RECORD
    count : INTEGER;
    info  : ARRAY [0..19] OF Info;
  END;

TYPE
  ThreadInfo = RECORD
    id          : INTEGER;
    stack_start : ADDRESS;
    stack_stop  : ADDRESS;
    reg_start   : ADDRESS;
    reg_stop    : ADDRESS;
    dump        : BOOLEAN;
  END;

TYPE
  VisitStack = ARRAY [0..10000] OF ADDRESS;

CONST
  MapGrain = 2 * BYTESIZE (RT0.RefHeader);  (* = 1 bit in the map *)
  MapBitsPerHeapPage = RTHeapRep.BytesPerPage DIV MapGrain;
  MapWordsPerHeapPage = MapBitsPerHeapPage DIV BITSIZE (Word.T);

VAR
  units       : InfoSet;
  unit_roots  : InfoSet;
  stacks      : InfoSet;
  stack_roots : InfoSet;
  stack_pages : InfoSet;
  map         : UNTRACED REF ARRAY OF Word.T;
  heap_min    : ADDRESS;
  heap_max    : ADDRESS;
  visit       : Info;
  visit_stack : UNTRACED REF VisitStack;
  top_of_stack: INTEGER;
  n_overflows : INTEGER;
  last_alloc  : ADDRESS;
  outerVisitor: RTHeapMap.Visitor := NIL;
  innerVisitor: RTHeapMap.Visitor := NIL;
  rootVisitor : RTHeapMap.Visitor := NIL;
  self_id     : INTEGER;
  n_threads   : INTEGER;
  threads     : ARRAY [0..199] OF ThreadInfo;

PROCEDURE <A NAME="ReportReachable"><procedure>ReportReachable</procedure></A> () =
  CONST MByte = 1024 * 1024;
  BEGIN
    (* allocate space for the stats *)
    outerVisitor := NEW (RTHeapMap.Visitor, apply := Visit);
    innerVisitor := NEW (RTHeapMap.Visitor, apply := InnerVisit);
    rootVisitor  := NEW (RTHeapMap.Visitor, apply := VisitRoot);
    visit_stack  := NEW (UNTRACED REF VisitStack);
    map := NEW (UNTRACED REF ARRAY OF Word.T,
                 (RTHeapRep.p1 - RTHeapRep.p0) * MapWordsPerHeapPage);

    (* initialize the globals *)
    units.count       := 0;
    unit_roots.count  := 0;
    stacks.count      := 0;
    stack_roots.count := 0;
    stack_pages.count := 0;
    top_of_stack      := 0;
    n_overflows       := 0;
    n_threads         := 0;

    (* freeze the world *)
    RTCollector.Disable ();
    RTOS.LockHeap (); (* freeze the heap *)

    (* capture the heap limits *)
    heap_min  := LOOPHOLE (RTHeapRep.p0 * RTHeapRep.BytesPerPage, ADDRESS);
    heap_max  := LOOPHOLE (RTHeapRep.p1 * RTHeapRep.BytesPerPage, ADDRESS);

    PutText (&quot;\nHEAP: &quot;);
    PutAddr (heap_min);
    PutText (&quot; .. &quot;);
    PutAddr (heap_max);
    PutText (&quot; =&gt; &quot;);
    PutInt ((heap_max - heap_min) DIV MByte);
    PutText (&quot;.&quot;);
    PutInt ((heap_max - heap_min) * 10 DIV MByte MOD 10);
    PutText (&quot; Mbytes\n&quot;);

    (* find the edge of the new space *)
    last_alloc := LOOPHOLE (NEW (REF INTEGER), ADDRESS);

    (* capture the thread info *)
    GetThreads ();

    FOR i := 0 TO RTModule.Count() - 1 DO GetUnitStats (i); END;
    FOR i := 0 TO RTModule.Count() - 1 DO GetUnitRootStats (i); END;
    FOR i := 0 TO n_threads-1          DO GetThreadStats (threads[i]); END;
    FOR i := 0 TO n_threads-1          DO GetThreadRootStats (threads[i]); END;
    FOR i := 0 TO n_threads-1          DO GetThreadPageStats (threads[i]); END;

    IF (n_overflows &gt; 0) THEN
      PutText (&quot;  ** warning: &quot;);
      PutInt (n_overflows);
      PutText (&quot; paths, longer than &quot;);
      PutInt (NUMBER (VisitStack));
      PutText (&quot; REFs, were truncated.\n&quot;);
    END;

    ReportUnits ();
    ReportUnitRoots ();

    ReportStacks ();
    ReportStackRoots ();
    ReportStackPages ();

    DumpStacks ();
    RTIO.Flush ();

    (* thaw the world *)
    DISPOSE (visit_stack);
    DISPOSE (map);
    RTOS.UnlockHeap (); (* unfreeze the heap *)
    RTCollector.Enable ();
  END ReportReachable;
</PRE>------------------------------------------------------------ REF visits ---

<P><PRE>PROCEDURE <A NAME="ResetVisitCounts"><procedure>ResetVisitCounts</procedure></A> () =
  BEGIN
    visit.n_objects := 0;
    visit.n_bytes   := 0;
    top_of_stack    := 0;
    RTMisc.Zero (ADR (map[0]), BYTESIZE (map^));
  END ResetVisitCounts;

PROCEDURE <A NAME="AddVisit"><procedure>AddVisit</procedure></A> (VAR s: InfoSet) =
  VAR n: INTEGER;
  BEGIN
    (* if the set isn't full, make room for this visit *)
    IF (s.count &lt; NUMBER (s.info)) THEN
      s.info[s.count].n_bytes := -1;
      INC (s.count);
    END;

    (* find where to insert this visit *)
    n := s.count-1;
    WHILE (n &gt;= 0) AND (s.info[n].n_bytes &lt; visit.n_bytes) DO
      IF (n &lt; LAST(s.info)) THEN s.info[n+1] := s.info[n]; END;
      DEC (n);
    END;
    INC (n);

    (* insert the new root *)
    IF (n &lt; s.count) THEN  s.info[n] := visit;  END;
  END AddVisit;

PROCEDURE <A NAME="Visit"><procedure>Visit</procedure></A> (&lt;*UNUSED*&gt; self: RTHeapMap.Visitor;  loc: ADDRESS) =
  BEGIN
    InnerVisit (NIL, loc);
    WHILE (top_of_stack &gt; 0) DO
      DEC (top_of_stack);
      RTHeapMap.WalkRef (visit_stack[top_of_stack], innerVisitor);
    END;
  END Visit;

PROCEDURE <A NAME="InnerVisit"><procedure>InnerVisit</procedure></A> (&lt;*UNUSED*&gt; self: RTHeapMap.Visitor;  loc: ADDRESS) =
  CONST Mask = ADRSIZE (RT0.RefHeader) - 1; (* assume it's 2^k-1 for some k *)
  VAR ptr : UNTRACED REF ADDRESS := loc;
  VAR ref : ADDRESS := ptr^;
  VAR header: RTHeapMap.ObjectPtr;
  VAR cell, word, bit, mask, typecode: INTEGER;
  BEGIN
    header := ref - ADRSIZE (RT0.RefHeader);
    IF (heap_min &lt;= ref) AND (ref &lt; heap_max)
      AND (Word.And (LOOPHOLE(ref, INTEGER), Mask) = 0) THEN
      typecode := header.typecode;
      IF (0 &lt; typecode) AND (typecode &lt; RT0u.nTypes) THEN
        cell := (ref - heap_min) DIV MapGrain;
        word := cell DIV BITSIZE (Word.T);
        bit  := cell - word * BITSIZE (Word.T);
        mask := Word.LeftShift (1, bit);
        IF (Word.And (mask, map[word]) = 0) THEN
          (* this is a new ref... *)
          map[word] := Word.Or (mask, map[word]);
          INC (visit.n_objects);
          INC (visit.n_bytes, DataSize (header) + BYTESIZE (RT0.RefHeader));
          IF (top_of_stack &lt; NUMBER (VisitStack)) THEN
            visit_stack [top_of_stack] := header;
            INC (top_of_stack);
          ELSE
            INC (n_overflows);
          END;
        END;
      END;
    END;
  END InnerVisit;

PROCEDURE <A NAME="DataSize"><procedure>DataSize</procedure></A> (h: RTHeapMap.ObjectPtr): CARDINAL =
  VAR
    res : INTEGER;
    tc  : RT0.Typecode := h.typecode;
    def : RT0.TypeDefn;
  BEGIN
    IF tc = RTHeapRep.Fill_1_type THEN RETURN 0; END;
    IF tc = RTHeapRep.Fill_N_type THEN
      res := LOOPHOLE(h + ADRSIZE(RT0.RefHeader), UNTRACED REF INTEGER)^;
      RETURN res - BYTESIZE(RT0.RefHeader);
    END;
    def := RTType.Get (tc);
    IF def.nDimensions = 0 THEN
      (* the typecell datasize tells the truth *)
      RETURN def.dataSize;
    END;
    (* ELSE, the referent is an open array; it has the following layout:
|         pointer to the elements (ADDRESS)
|         size 1
|         ....
|         size n
|         optional padding
|         elements
|         ....
       where n is the number of open dimensions (given by the definition)
       and each size is the number of elements along the dimension *)
    VAR
      sizes: UNTRACED REF INTEGER := h + ADRSIZE(RT0.RefHeader)
                                       + ADRSIZE(ADDRESS);  (* ^ elt pointer*)
    BEGIN
      res := 1;
      FOR i := 0 TO def.nDimensions - 1 DO
        res := res * sizes^;
        INC(sizes, ADRSIZE(sizes^));
      END;
      res := res * def.elementSize;
    END;
    res := RTMisc.Upper(res + def.dataSize, BYTESIZE(RT0.RefHeader));
    RETURN res;
  END DataSize;

PROCEDURE <A NAME="TypeName"><procedure>TypeName</procedure></A> (ref: ADDRESS): TEXT =
  CONST Mask = ADRSIZE (RT0.RefHeader) - 1; (* assume it's 2^k-1 for some k *)
  VAR header: RTHeapMap.ObjectPtr;
  VAR typecode: INTEGER;
  BEGIN
    header := ref - ADRSIZE (RT0.RefHeader);
    IF (Word.And (LOOPHOLE (header, INTEGER), Mask) = 0) (* =&gt; aligned *)
      AND (heap_min &lt;= ref) AND (ref &lt; heap_max) THEN
      typecode := header.typecode;
      IF (0 &lt; typecode) AND (typecode &lt; RT0u.nTypes) THEN
        RETURN RTTypeSRC.TypecodeName (typecode);
      END;
    END;
    RETURN &quot;?&quot;;
  END TypeName;
</PRE>----------------------------------------------------------------- units ---

<P><PRE>PROCEDURE <A NAME="GetUnitStats"><procedure>GetUnitStats</procedure></A> (n: CARDINAL) =
  BEGIN
    visit.module     := RTModule.Get (n);
    visit.thread_id  := -1;
    visit.location   := NIL;
    visit.ref        := NIL;
    ResetVisitCounts ();
    RTHeapMap.WalkModuleGlobals (outerVisitor, n);
    AddVisit (units);
  END GetUnitStats;

PROCEDURE <A NAME="GetUnitRootStats"><procedure>GetUnitRootStats</procedure></A> (n: CARDINAL) =
  BEGIN
    visit.module     := RTModule.Get (n);
    visit.thread_id  := -1;
    visit.location   := NIL;
    visit.ref        := NIL;
    RTHeapMap.WalkModuleGlobals (rootVisitor, n);
  END GetUnitRootStats;

PROCEDURE <A NAME="VisitRoot"><procedure>VisitRoot</procedure></A> (&lt;*UNUSED*&gt; self: RTHeapMap.Visitor;  root: ADDRESS) =
  VAR p: UNTRACED REF ADDRESS := root;
  BEGIN
    visit.location := root;
    visit.ref      := p^;
    ResetVisitCounts ();
    Visit (NIL, root);
    AddVisit (unit_roots);
  END VisitRoot;
</PRE>--------------------------------------------------------------- threads ---

<P><PRE>VAR is_registers: BOOLEAN := FALSE;
    mark_addr: ADDRESS;

PROCEDURE <A NAME="GetThreads"><procedure>GetThreads</procedure></A> () =
  VAR i: INTEGER;
  BEGIN
    self_id := -1;
    mark_addr := ADR (i);
    ThreadF.ProcessStacks (GetThread);
    RTIO.PutText (&quot;Threads: &quot;);
    RTIO.PutInt (n_threads);
    IF (n_threads &gt; NUMBER (threads)) THEN
      RTIO.PutText (&quot;  (&quot;);
      RTIO.PutInt (n_threads - NUMBER (threads));
      RTIO.PutText (&quot; ignored)&quot;);
      n_threads := NUMBER (threads);
    END;
    RTIO.PutChar ('\n');
  END GetThreads;

PROCEDURE <A NAME="GetThread"><procedure>GetThread</procedure></A> (start, stop: ADDRESS) =
  BEGIN
    IF (start &lt;= mark_addr) AND (mark_addr &lt;= stop) THEN
      self_id := n_threads;
    END;
    IF (n_threads &lt; NUMBER (threads)) THEN
      WITH z = threads[n_threads] DO
        z.id := n_threads;
        z.dump := FALSE;
        IF is_registers
          THEN z.reg_start   := start;  z.reg_stop   := stop;
          ELSE z.stack_start := start;  z.stack_stop := stop;
        END;
      END;
    END;
    IF (is_registers) THEN INC (n_threads); END;
    is_registers := NOT is_registers;
  END GetThread;

PROCEDURE <A NAME="GetThreadStats"><procedure>GetThreadStats</procedure></A> (READONLY ti: ThreadInfo) =
  BEGIN
    visit.module     := NIL;
    visit.thread_id  := ti.id;
    visit.location   := NIL;
    visit.ref        := NIL;

    ResetVisitCounts ();
    ScanPages (ti.stack_start, ti.stack_stop);
    ScanPages (ti.reg_start,   ti.reg_stop);
    AddVisit (stacks);
  END GetThreadStats;

PROCEDURE <A NAME="ScanPages"><procedure>ScanPages</procedure></A> (start, stop: ADDRESS) =
  VAR fp := start;  p: ADDRESS;  page: INTEGER;
  BEGIN
    (* scan the stack or registers *)
    WHILE fp &lt;= stop DO
      p := LOOPHOLE(fp, UNTRACED REF ADDRESS)^;
      IF heap_min &lt;= p AND p &lt; heap_max THEN
        page := (p - heap_min) DIV RTHeapRep.BytesPerPage;
        IF RTHeapRep.desc[page].space = RTHeapRep.Space.Current THEN
          VisitPage (page);
        END;
      END;
      INC(fp, RTMachine.PointerAlignment);
    END;
  END ScanPages;

PROCEDURE <A NAME="GetThreadRootStats"><procedure>GetThreadRootStats</procedure></A> (READONLY ti: ThreadInfo) =
  BEGIN
    visit.module     := NIL;
    visit.thread_id  := ti.id;
    visit.location   := NIL;
    visit.ref        := NIL;
    ScanThreadRoots (ti.stack_start, ti.stack_stop, on_stack := TRUE);
    ScanThreadRoots (ti.reg_start,   ti.reg_stop,   on_stack := FALSE);
  END GetThreadRootStats;

PROCEDURE <A NAME="ScanThreadRoots"><procedure>ScanThreadRoots</procedure></A> (start, stop: ADDRESS;  on_stack: BOOLEAN) =
  VAR fp := start;  p: ADDRESS;  page: INTEGER;
  BEGIN
    WHILE fp &lt;= stop DO
      p := LOOPHOLE(fp, UNTRACED REF ADDRESS)^;
      IF heap_min &lt;= p AND p &lt; heap_max THEN
        page := (p - heap_min) DIV RTHeapRep.BytesPerPage;
        IF RTHeapRep.desc[page].space = RTHeapRep.Space.Current THEN
          IF on_stack
            THEN visit.location := fp;
            ELSE visit.location := NIL;
          END;
          visit.ref := p;
          ResetVisitCounts ();
          Visit (NIL, fp);
          AddVisit (stack_roots);
        END;
      END;
      INC(fp, RTMachine.PointerAlignment);
    END;
  END ScanThreadRoots;

PROCEDURE <A NAME="GetThreadPageStats"><procedure>GetThreadPageStats</procedure></A> (READONLY ti: ThreadInfo) =
  BEGIN
    visit.module     := NIL;
    visit.thread_id  := ti.id;
    visit.location   := NIL;
    visit.ref        := NIL;
    ScanThreadPageRoots (ti.stack_start, ti.stack_stop, on_stack := TRUE);
    ScanThreadPageRoots (ti.reg_start,   ti.reg_stop,   on_stack := FALSE);
  END GetThreadPageStats;

PROCEDURE <A NAME="ScanThreadPageRoots"><procedure>ScanThreadPageRoots</procedure></A> (start, stop: ADDRESS;  on_stack: BOOLEAN) =
  VAR fp := start;  p: ADDRESS;  page: INTEGER;
  BEGIN
    WHILE fp &lt;= stop DO
      p := LOOPHOLE(fp, UNTRACED REF ADDRESS)^;
      IF heap_min &lt;= p AND p &lt; heap_max THEN
        page := (p - heap_min) DIV RTHeapRep.BytesPerPage;
        IF RTHeapRep.desc[page].space = RTHeapRep.Space.Current THEN
          IF on_stack
            THEN visit.location := fp;
            ELSE visit.location := NIL;
          END;
          visit.ref := p;
          ResetVisitCounts ();
          VisitPage (page);
          AddVisit (stack_pages);
        END;
      END;
      INC(fp, RTMachine.PointerAlignment);
    END;
  END ScanThreadPageRoots;

PROCEDURE <A NAME="VisitPage"><procedure>VisitPage</procedure></A> (page: INTEGER) =
  VAR start, stop: ADDRESS;  h: RTHeapMap.ObjectPtr;  ref: ADDRESS;
  BEGIN
    (* find the address limits of this &quot;page&quot; *)
    WHILE (page &gt; 0)
      AND (RTHeapRep.desc[page].space = RTHeapRep.Space.Current)
      AND (RTHeapRep.desc[page].continued) DO
      DEC (page);
    END;
    start := heap_min + page * RTHeapRep.BytesPerPage;
    REPEAT
      INC (page);
    UNTIL (page &gt;= RTHeapRep.p1-RTHeapRep.p0)
       OR (RTHeapRep.desc[page].space # RTHeapRep.Space.Current)
       OR (NOT RTHeapRep.desc[page].continued);
    stop := heap_min + page * RTHeapRep.BytesPerPage;

    IF (start &lt;= last_alloc) AND (last_alloc &lt; stop) THEN
      (* we're on the allocator's partial page... *)
      stop := last_alloc;
    END;

    (* visit each object on the page *)
    h := start;
    WHILE (h &lt; stop) AND (h.typecode # 0) DO
      ref := h + ADRSIZE (RT0.RefHeader);
      Visit (NIL, ADR (ref));
      INC (h, DataSize (h) + ADRSIZE (RT0.RefHeader));
    END;
  END VisitPage;
</PRE>--------------------------------------------------------------- reports ---

<P><PRE>PROCEDURE <A NAME="ReportUnits"><procedure>ReportUnits</procedure></A> () =
  BEGIN
    PutText (&quot;\nModule globals:\n&quot;);
    PutText (&quot; # objects   # bytes  unit\n&quot;);
    PutText (&quot; ---------  --------  -----------------\n&quot;);
    FOR i := 0 TO units.count-1 DO
      WITH m = units.info[i] DO
        IF (m.n_bytes &gt; 0) THEN
          PutInt (m.n_objects, 10);
          PutInt (m.n_bytes, 10);
          PutText (&quot;  &quot;);
          PutStr  (PathTail (m.module.file));
          PutText (&quot;\n&quot;);
        END;
      END;
    END;
  END ReportUnits;

PROCEDURE <A NAME="ReportUnitRoots"><procedure>ReportUnitRoots</procedure></A> () =
  BEGIN
    PutText (&quot;\nGlobal variable roots:\n&quot;);
    PutText (&quot; # objects   # bytes         ref type                location\n&quot;);
    PutText (&quot; ---------  --------  ---------- -----------------   ------------------------\n&quot;);
    FOR i := 0 TO unit_roots.count-1 DO
      WITH r = unit_roots.info[i] DO
        IF (r.n_bytes &gt; 0) THEN
          PutInt  (r.n_objects, 10);
          PutInt  (r.n_bytes, 10);
          PutText (&quot;  &quot;);
          PutAddr (r.ref);
          PutText (&quot; &quot;);
          PadText (TypeName (r.ref), 18);
          PutText (&quot;  &quot;);
          PutStr  (PathTail (r.module.file));
          PutText (&quot; + &quot;);
          PutInt (r.location - r.module);
          PutText (&quot;\n&quot;);
        END;
      END;
    END;
  END ReportUnitRoots;

PROCEDURE <A NAME="ReportStacks"><procedure>ReportStacks</procedure></A> () =
  BEGIN
    PutText (&quot;\nThread stacks (conservative page scan):\n&quot;);
    PutText (&quot; # objects   # bytes  thread  [stack bounds]\n&quot;);
    PutText (&quot; ---------  --------  -------------------------------\n&quot;);
    FOR i := 0 TO stacks.count-1 DO
      WITH t = stacks.info[i] DO
        IF (t.n_bytes &gt; 0) THEN
          PutInt (t.n_objects, 10);
          PutInt (t.n_bytes, 10);
          PutText (&quot;  T.&quot;);
          PutInt (t.thread_id, 1);
          PutText (&quot;  [&quot;);
          PutAddr (threads[t.thread_id].stack_start);
          PutText (&quot; .. &quot;);
          PutAddr (threads[t.thread_id].stack_stop);
          PutText (&quot;]\n&quot;);
          threads[t.thread_id].dump := TRUE;
        END;
      END;
    END;
  END ReportStacks;

PROCEDURE <A NAME="ReportStackRoots"><procedure>ReportStackRoots</procedure></A> () =
  BEGIN
    PutText (&quot;\nThread stack roots (optimistic):\n&quot;);
    ReportStackInfo (stack_roots);
  END ReportStackRoots;

PROCEDURE <A NAME="ReportStackPages"><procedure>ReportStackPages</procedure></A> () =
  BEGIN
    PutText (&quot;\nThread stack roots (conservative page scan):\n&quot;);
    ReportStackInfo (stack_pages);
  END ReportStackPages;

PROCEDURE <A NAME="ReportStackInfo"><procedure>ReportStackInfo</procedure></A> (READONLY s: InfoSet) =
  BEGIN
    PutText (&quot; # objects   # bytes         ref type                location\n&quot;);
    PutText (&quot; ---------  --------  ---------- -----------------   ------------------------\n&quot;);
    FOR i := 0 TO s.count-1 DO
      WITH r = s.info[i] DO
        IF (r.n_bytes &gt; 0) THEN
          PutInt  (r.n_objects, 10);
          PutInt  (r.n_bytes, 10);
          PutText (&quot;  &quot;);
          PutAddr (r.ref);
          PutText (&quot; &quot;);
          PadText (TypeName (r.ref), 18);
          PutText (&quot;  &quot;);
          IF (r.location # NIL) THEN
            PutText (&quot;sp+&quot;);
            PutInt  (r.location - threads[r.thread_id].stack_start);
          ELSE
            PutText (&quot;register&quot;);
          END;
          PutText (&quot; in T.&quot;);
          PutInt  (r.thread_id);
          PutText (&quot;\n&quot;);
          threads[r.thread_id].dump := TRUE;
        END;
      END;
    END;
  END ReportStackInfo;
</PRE>----------------------------------------------------------- stack dumps ---

<P><PRE>VAR
  conservative_cutoff : INTEGER;
  optimistic_cutoff   : INTEGER;

PROCEDURE <A NAME="DumpStacks"><procedure>DumpStacks</procedure></A> () =
  BEGIN
    conservative_cutoff := MAX (MinInfoBytes (stack_pages) DIV 2, 1024);
    optimistic_cutoff   := MAX (MinInfoBytes (stack_roots) DIV 2, 1024);
    PutText (&quot;\n-------------------------------------------------------\n&quot;);
    PutText (&quot;Thread stack dumps with references that reach\nat least:\n&quot;);
    PutInt  (optimistic_cutoff, 10);
    PutText (&quot; bytes under the optimistic scan or\n&quot;);
    PutInt  (conservative_cutoff, 10);
    PutText (&quot; bytes under the conservative scan.\n\n&quot;);

    FOR i := 0 TO n_threads-1 DO
      IF threads[i].dump THEN DumpStack (threads[i]); END;
    END;
  END DumpStacks;

PROCEDURE <A NAME="MinInfoBytes"><procedure>MinInfoBytes</procedure></A> (READONLY s: InfoSet): INTEGER =
  VAR x := LAST (INTEGER);
  BEGIN
    FOR i := 0 TO s.count-1 DO
      x := MIN (x, s.info[i].n_bytes);
    END;
    RETURN x;
  END MinInfoBytes;

PROCEDURE <A NAME="DumpStack"><procedure>DumpStack</procedure></A> (READONLY ti: ThreadInfo) =
  CONST Max_proc = 4096;  (* good enough for 99% of the procedures *)
  VAR
    fp, p: ADDRESS;
    page : INTEGER;
    cons_cnt, cons_bytes : INTEGER;
    opt_cnt, opt_bytes   : INTEGER;
    have_frames : BOOLEAN;
    cur, prev: RTStack.Frame;
    proc_start: RTProcedure.Proc;
    file, proc_name: RTProcedureSRC.Name;
  BEGIN
    have_frames := RTStack.Has_walker;
    IF RTStack.Has_walker THEN
      IF (ti.id = self_id) THEN
        RTStack.CurrentFrame (cur);
      ELSE
        RTStack.GetThreadFrame (cur, ti.reg_start, ti.reg_stop - ti.reg_start);
      END;
      IF cur.pc = NIL THEN have_frames := FALSE; END;
    END;

    PutText (&quot;-------------------------------------------------\n&quot;);
    PutText (&quot;Thread T.&quot;);
    PutInt  (ti.id, 1);
    PutText (&quot;  stack [&quot;);
    PutAddr (ti.stack_start);
    PutText (&quot; .. &quot;);
    PutAddr (ti.stack_stop);
    PutText (&quot;]\n\n&quot;);
    PutText (&quot; stack    optimistic    conservative\n&quot;);
    PutText (&quot;offset  #objs # bytes  #objs # bytes  ref\n&quot;);
    PutText (&quot;------  ----- -------  ----- -------  ------------\n&quot;);
    fp := ti.stack_start;
    WHILE (fp &lt;= ti.stack_stop) DO

      IF have_frames THEN
        WHILE (cur.sp &lt;= fp) AND (cur.pc # NIL) DO
          RTProcedureSRC.FromPC (cur.pc, proc_start, file, proc_name);
          IF (proc_start # NIL) AND (cur.pc - proc_start &lt; Max_proc) THEN
            PutInt  (cur.sp - ti.stack_start, 5);
            PutText (&quot;                                 --&gt; &quot;);
            PutStr  (proc_name);
            IF (cur.pc # proc_start) THEN
              PutText (&quot; + &quot;);
              PutInt (cur.pc - proc_start);
            END;
            IF (file # NIL) THEN
              PutText (&quot; in &quot;);
              PutStr (PathTail (file));
            END;
            PutText (&quot;\n&quot;);
          END;
          RTStack.PreviousFrame (cur, prev);
          cur := prev;
        END;
      END;

      p := LOOPHOLE(fp, UNTRACED REF ADDRESS)^;
      IF heap_min &lt;= p AND p &lt; heap_max THEN
        page := (p - heap_min) DIV RTHeapRep.BytesPerPage;
        IF RTHeapRep.desc[page].space = RTHeapRep.Space.Current THEN
          visit.location := fp;
          visit.ref := p;

          (* make the conservative scan *)
          ResetVisitCounts ();
          VisitPage (page);
          cons_bytes := visit.n_bytes;
          cons_cnt   := visit.n_objects;

          (* make the optimistic scan *)
          ResetVisitCounts ();
          Visit (NIL, fp);
          opt_bytes := visit.n_bytes;
          opt_cnt   := visit.n_objects;

          IF (cons_bytes &gt;= conservative_cutoff)
            OR (opt_bytes &gt;= optimistic_cutoff) THEN
            (* report this ref! *)
            PutInt  (fp - ti.stack_start, 5);
            PutInt  (opt_cnt, 8);
            PutInt  (opt_bytes, 8);
            PutInt  (cons_cnt, 7);
            PutInt  (cons_bytes, 8);
            PutText (&quot;  &quot;);
            PutAddr (p);
            PutText (&quot; &quot;);
            PutText (TypeName (p));
            PutText (&quot;\n&quot;);
          END;
        END;
      END;

      IF NOT have_frames THEN
        (* maybe this stack element is a PC we should print *)
        RTProcedureSRC.FromPC (p, proc_start, file, proc_name);
        IF (proc_start # NIL) AND (p - proc_start &lt; Max_proc) THEN
          PutInt  (fp - ti.stack_start, 5);
          PutText (&quot;                                 [PC &quot;);
          PutAddr (p);
          PutText (&quot;] &quot;);
          PutStr  (proc_name);
          IF (p # proc_start) THEN
            PutText (&quot; + &quot;);
            PutInt (p - proc_start);
          END;
          IF (file # NIL) THEN
            PutText (&quot; in &quot;);
            PutStr (PathTail (file));
          END;
          PutText (&quot;\n&quot;);
        END;
      END;

      INC (fp, RTMachine.PointerAlignment);
    END;
  END DumpStack;
</PRE>--------------------------------------------------------- low-level I/O ---

<P><PRE>PROCEDURE <A NAME="PathTail"><procedure>PathTail</procedure></A> (a: ADDRESS): ADDRESS =
  VAR p0 : UNTRACED REF CHAR := a;  p := p0;
  BEGIN
    IF (p0 = NIL) THEN RETURN NIL END;
    WHILE (p^ # '\000') DO
      IF (p^ = '/') THEN p0 := p + ADRSIZE (p^); END;
      INC (p, ADRSIZE (p^));
    END;
    RETURN p0;
  END PathTail;

PROCEDURE <A NAME="PutStr"><procedure>PutStr</procedure></A> (s: ADDRESS) =
  BEGIN
    IF (s = NIL) THEN RETURN END;
    RTIO.PutString (s);
  END PutStr;

PROCEDURE <A NAME="PadText"><procedure>PadText</procedure></A> (t: TEXT;  width := 0) =
  VAR len := Text.Length (t);
  BEGIN
    RTIO.PutText (t);
    WHILE (len &lt; width) DO
      RTIO.PutChar (' ');
      INC (len);
    END;
  END PadText;

BEGIN
END RTHeapStats.
</PRE>
</inModule>
<HR>
<A NAME="x1">interface RT0u is in:
</A><UL>
<LI><A HREF="../POSIX/RT0u.i3.html#0TOP0">runtime/src/POSIX/RT0u.i3</A>
<LI><A HREF="../WIN32/RT0u.i3.html#0TOP0">runtime/src/WIN32/RT0u.i3</A>
</UL>
<P>
<HR>
<A NAME="x2">interface RTMachine is in:
</A><UL>
<LI><A HREF="../AIX386/RTMachine.i3.html#0TOP0">runtime/src/AIX386/RTMachine.i3</A>
<LI><A HREF="../ALPHA_OSF/RTMachine.i3.html#0TOP0">runtime/src/ALPHA_OSF/RTMachine.i3</A>
<LI><A HREF="../AP3000/RTMachine.i3.html#0TOP0">runtime/src/AP3000/RTMachine.i3</A>
<LI><A HREF="../ARM/RTMachine.i3.html#0TOP0">runtime/src/ARM/RTMachine.i3</A>
<LI><A HREF="../DS3100/RTMachine.i3.html#0TOP0">runtime/src/DS3100/RTMachine.i3</A>
<LI><A HREF="../FreeBSD/RTMachine.i3.html#0TOP0">runtime/src/FreeBSD/RTMachine.i3</A>
<LI><A HREF="../FreeBSD2/RTMachine.i3.html#0TOP0">runtime/src/FreeBSD2/RTMachine.i3</A>
<LI><A HREF="../HP300/RTMachine.i3.html#0TOP0">runtime/src/HP300/RTMachine.i3</A>
<LI><A HREF="../HPPA/RTMachine.i3.html#0TOP0">runtime/src/HPPA/RTMachine.i3</A>
<LI><A HREF="../IBMR2/RTMachine.i3.html#0TOP0">runtime/src/IBMR2/RTMachine.i3</A>
<LI><A HREF="../IBMRT/RTMachine.i3.html#0TOP0">runtime/src/IBMRT/RTMachine.i3</A>
<LI><A HREF="../IRIX5/RTMachine.i3.html#0TOP0">runtime/src/IRIX5/RTMachine.i3</A>
<LI><A HREF="../LINUX/RTMachine.i3.html#0TOP0">runtime/src/LINUX/RTMachine.i3</A>
<LI><A HREF="../LINUXELF/RTMachine.i3.html#0TOP0">runtime/src/LINUXELF/RTMachine.i3</A>
<LI><A HREF="../NEXT/RTMachine.i3.html#0TOP0">runtime/src/NEXT/RTMachine.i3</A>
<LI><A HREF="../NT386/RTMachine.i3.html#0TOP0">runtime/src/NT386/RTMachine.i3</A>
<LI><A HREF="../OKI/RTMachine.i3.html#0TOP0">runtime/src/OKI/RTMachine.i3</A>
<LI><A HREF="../SEQUENT/RTMachine.i3.html#0TOP0">runtime/src/SEQUENT/RTMachine.i3</A>
<LI><A HREF="../SOLgnu/RTMachine.i3.html#0TOP0">runtime/src/SOLgnu/RTMachine.i3</A>
<LI><A HREF="../SOLsun/RTMachine.i3.html#0TOP0">runtime/src/SOLsun/RTMachine.i3</A>
<LI><A HREF="../SPARC/RTMachine.i3.html#0TOP0">runtime/src/SPARC/RTMachine.i3</A>
<LI><A HREF="../SUN3/RTMachine.i3.html#0TOP0">runtime/src/SUN3/RTMachine.i3</A>
<LI><A HREF="../SUN386/RTMachine.i3.html#0TOP0">runtime/src/SUN386/RTMachine.i3</A>
<LI><A HREF="../UMAX/RTMachine.i3.html#0TOP0">runtime/src/UMAX/RTMachine.i3</A>
<LI><A HREF="../VAX/RTMachine.i3.html#0TOP0">runtime/src/VAX/RTMachine.i3</A>
</UL>
<P>
<HR>
<A NAME="x3">interface ThreadF is in:
</A><UL>
<LI><A HREF="../../../thread/src/NOOP/ThreadF.i3.html#0TOP0">thread/src/NOOP/ThreadF.i3</A>
<LI><A HREF="../../../thread/src/POSIX/ThreadF.i3.html#0TOP0">thread/src/POSIX/ThreadF.i3</A>
<LI><A HREF="../../../thread/src/WIN32/ThreadF.i3.html#0TOP0">thread/src/WIN32/ThreadF.i3</A>
</UL>
<P>
<PRE>























</PRE>
</BODY>
</HTML>
