<HTML>
<HEAD>
<TITLE>SRC Modula-3: runtime/src/common/RTCollector.m3</TITLE>
</HEAD>
<BODY>
<A NAME="0TOP0">
<H2>runtime/src/common/RTCollector.m3</H2></A><HR>
<inModule>
<PRE><A HREF="../../../COPYRIGHT.html">Copyright (C) 1994, Digital Equipment Corp.</A>
</PRE><BLOCKQUOTE><EM>                                                           </EM></BLOCKQUOTE><PRE>
</PRE> portions Copyright 1996, Critical Mass, Inc.              
                                                           

<P><PRE>UNSAFE MODULE <module>RTCollector</module> EXPORTS <A HREF="RTCollector.i3.html"><implements>RTCollector</A></implements>, <A HREF="RTCollectorSRC.i3.html"><implements>RTCollectorSRC</A></implements>,
                                  <A HREF="RTHeapRep.i3.html"><implements>RTHeapRep</A></implements>, <A HREF="RTWeakRef.i3.html"><implements>RTWeakRef</A></implements>;

IMPORT <A HREF="RT0.i3.html">RT0</A>, <A HREF="#x1">RT0u</A>, <A HREF="RTHeapEvent.i3.html">RTHeapEvent</A>, <A HREF="RTHeapDep.i3.html">RTHeapDep</A>, <A HREF="RTHeapMap.i3.html">RTHeapMap</A>, <A HREF="#x2">RTMachine</A>;
IMPORT <A HREF="RTMisc.i3.html">RTMisc</A>, <A HREF="RTOS.i3.html">RTOS</A>, <A HREF="RTParams.i3.html">RTParams</A>, <A HREF="#x3">RTPerfTool</A>, <A HREF="RTProcess.i3.html">RTProcess</A>, <A HREF="RTType.i3.html">RTType</A>;
IMPORT <A HREF="../../../word/src/Word.i3.html">Word</A>, <A HREF="../../../C/src/Common/Cstdlib.i3.html">Cstdlib</A>, <A HREF="../../../thread/src/Common/Thread.i3.html">Thread</A>, <A HREF="#x4">ThreadF</A>;

FROM <A HREF="RT0.i3.html">RT0</A> IMPORT Typecode, TypeDefn;
FROM <A HREF="RTIO.i3.html">RTIO</A> IMPORT PutText, PutInt;
</PRE> The allocator/garbage collector for the traced heap is an adaptation of
   the algorithm presented in the WRL Research Report 88/2, ``Compacting
   Garbage Collection with Ambiguous Roots'', by Joel F.  Bartlett; see
   this report for a detailed presentation.  John DeTreville modified it to
   be incremental, generational, and VM-synchronized.
<P>
   The allocator/collector for the untraced heap is simply malloc/free. 

<P> Much of the code below incorrectly assumes no difference between ADRSIZE
   and BYTESIZE. 

<P> In the following procedures, <CODE>RTType.Get(tc)</CODE> will fail if <CODE>tc</CODE> is not
   proper. 

<P>** RTCollector **

<P><PRE>PROCEDURE <A NAME="Disable"><procedure>Disable</procedure></A> () =
  BEGIN
    RTOS.LockHeap();
    BEGIN
      FinishVM();
      INC(disableCount);
      partialCollectionNext := FALSE;
    END;
    RTOS.UnlockHeap();
    IF perfOn THEN PerfAllow(); END;
  END Disable;

PROCEDURE <A NAME="Enable"><procedure>Enable</procedure></A> () =
  BEGIN
    RTOS.LockHeap();
    BEGIN
      DEC(disableCount);
      CollectEnough();
    END;
    RTOS.UnlockHeap();
    IF perfOn THEN PerfAllow(); END;
  END Enable;

PROCEDURE <A NAME="DisableMotion"><procedure>DisableMotion</procedure></A> () =
  BEGIN
    RTOS.LockHeap();
    BEGIN
      INC(disableMotionCount);
    END;
    RTOS.UnlockHeap();
    IF perfOn THEN PerfAllow(); END;
  END DisableMotion;

PROCEDURE <A NAME="EnableMotion"><procedure>EnableMotion</procedure></A> () =
  BEGIN
    RTOS.LockHeap();
    BEGIN
      DEC(disableMotionCount);
      CollectEnough();
    END;
    RTOS.UnlockHeap();
    IF perfOn THEN PerfAllow(); END;
  END EnableMotion;

PROCEDURE <A NAME="Collect"><procedure>Collect</procedure></A> () =
  BEGIN
    RTOS.LockHeap();
    BEGIN
      FinishGC();
      StartGC();
      FinishGC();
    END;
    RTOS.UnlockHeap();
  END Collect;
</PRE>** RTCollectorSRC **

<P> StartCollection starts a total collection, if none is in progress and if
   collection and motion are enabled. 

<P><PRE>PROCEDURE <A NAME="StartCollection"><procedure>StartCollection</procedure></A> () =
  BEGIN
    RTOS.LockHeap();
    BEGIN
      CollectorOn();
      IF collectorState = CollectorState.Zero
           AND disableCount + disableMotionCount = 0 THEN
        partialCollectionNext := FALSE;
        REPEAT CollectSome(); UNTIL collectorState # CollectorState.Zero;
        IF NOT (incremental AND RTHeapDep.VM AND disableVMCount = 0) THEN
          REPEAT CollectSome(); UNTIL collectorState = CollectorState.Zero;
        END;
      END;
      CollectorOff();
    END;
    RTOS.UnlockHeap();
  END StartCollection;
</PRE> FinishCollection finishes the current collection, if one is on
   progress. 

<P><PRE>PROCEDURE <A NAME="FinishCollection"><procedure>FinishCollection</procedure></A> () =
  BEGIN
    RTOS.LockHeap();
    BEGIN
      CollectorOn();
      WHILE collectorState # CollectorState.Zero DO CollectSome(); END;
      CollectorOff();
    END;
    RTOS.UnlockHeap();
  END FinishCollection;
</PRE> DisableVM disables the use of VM protection.  While VM protection is
   disabled, no objects on the heap will be protected.

<P><PRE>PROCEDURE <A NAME="DisableVM"><procedure>DisableVM</procedure></A> () =
  BEGIN
    RTOS.LockHeap();
    BEGIN
      FinishVM();
      INC(disableVMCount);
    END;
    RTOS.UnlockHeap();
  END DisableVM;
</PRE> EnableVM reenables the use of VM protection if EnableVM has been called
   as many times as DisableVM.  It is a checked runtime error to call
   EnableVM more times than DisableVM. 

<P><PRE>PROCEDURE <A NAME="EnableVM"><procedure>EnableVM</procedure></A> () =
  BEGIN
    RTOS.LockHeap();
    BEGIN
      DEC(disableVMCount);
      CollectEnough();
    END;
    RTOS.UnlockHeap();
  END EnableVM;
</PRE> FinishVM is equivalent to DisableVM{}; EnableVM().  FinishVM unprotects
   all heap pages, and is intended for use from the debugger. 

<P><PRE>PROCEDURE <A NAME="FinishVM"><procedure>FinishVM</procedure></A> () =
  BEGIN
    RTOS.LockHeap();
    BEGIN
      FinishGC();
      CollectorOn();
      (* no gray pages now; only protected pages are in older generation *)
      FOR p := p0 TO p1 - 1 DO
        IF desc[p - p0].protected THEN Unprotect(p); END;
      END;
      CollectorOff();
    END;
    RTOS.UnlockHeap();
  END FinishVM;
</PRE> StartBackgroundCollection starts the background thread, if not already
   started 

<P><PRE>VAR startedBackground := FALSE;

PROCEDURE <A NAME="StartBackgroundCollection"><procedure>StartBackgroundCollection</procedure></A> () =
  VAR start := FALSE;
  BEGIN
    RTOS.LockHeap();
    BEGIN
      IF NOT startedBackground THEN
        start := TRUE;
        startedBackground := TRUE;
      END;
    END;
    RTOS.UnlockHeap();
    IF start THEN
      EVAL Thread.Fork(NEW(Thread.Closure, apply := BackgroundThread));
    END;
  END StartBackgroundCollection;
</PRE> ------------------------------- low-level allocation and collection 

<P> We assume that references (values of the types ADDRESS and REFANY) are
   the addresses of addressable locations and that locations with
   successive addresses are contiguous (that is, if a points to a
   n-locations referent then these n locations are at addresses a, a+1,
   ..., a+n-1).
<P>
   The memory is viewed as a collection of pages.  Each page has a number
   that identifies it, based on the addresses that are part of this page:
   page p contains the addresses p * BytesPerPage to (p+1) * BytesPerPage -
   1.
<P>
   The page size must be a multiple of the header size (see below).  Given
   our conventions about page boundaries, this implies that the first
   location of a page is properly aligned for a Header. 

<P> The array desc and the global variables p0, and p1 describe the pages
   that are part of the traced heap.  Either p0 and p1 are equal to Nil and
   no pages are allocated; or both are valid pages and page p is allocated
   iff
<P>
<PRE>
               p0 &lt;= p &lt; p1
           AND desc[p - p0] != Unallocated
</PRE>
   NUMBER (desc) must be equal to p1 - p0 if there are allocated pages.
   Index i in desc correspond to page i + p0; that is p0 is the number of
   the first page available in desc, and it must be in [p0 ..  p1) if there
   are allocated pages. 

<P> We keep the number of allocated pages in a global variable; it should
   satify the invariant:
<P>
<PRE>
          allocatedPages = sigma (i = p0, p1-1,
                                   space [i - p0] # Unallocated)
                                       if there are allocated pages,
                           = 0 otherwise.
</PRE>
   We also keep the number of active pages in a global; it satisfies:
<P>
<PRE>
          activePages = sigma (i = p0, p1-1,
                                space [i - p0] = nextSpace)
                                     if there are allocated pages,
                      = 0 otherwise. 


<P> </PRE>
Each referent is immediately preceded by a header that describes the
   type of the referent.  In the user world, this header is not visible;
   that is, a REFANY is the address of the referent, not the address of the
   header.
<P>
   Each referent is immediately followed by padding space so the combined
   size referent size + padding is a multiple of the header size.
   Actually, the low level routines are given a data size which is the sum
   of the referent size and padding size and assume this data size is a
   multiple of the header size.
<P>
   With this padding, addresses of headers and referent will always be
   multiple of ADRSIZE (Header).
<P>
   The combination of header/referent/padding space is called a <CODE>heap
   object</CODE>.  The size of a heap object is the size of the header, plus the
   size of the referent, plus the size of the padding.  The alignment of a
   heap object is the greatest of the alignment of header and the alignment
   of the referent.
<P>
   We make the following assumptions:
<P>
   - alignment of headers is such what the addressable location following
   any properly aligned header is properly aligned for the type ADDRESS;
   and, for every referent: referent adrSize + padding adrSize &gt;= ADRSIZE
   (ADDRESS)
<P>
   [During the garbage collection, we move heap objects.  But we need to
   keep the forwarding information somewhere.  This condition ensures that
   we can store the new address of the referent in the first word of the
   old referent.]
<P>
   - the pages are aligned more strictly than the headers (this means that
   the page size is a multiple of the header alignment).
<P>
   [We can put a header at the beginning of a page] 

<P><PRE>TYPE
  RefReferent = ADDRESS;

PROCEDURE <A NAME="HeaderOf"><procedure>HeaderOf</procedure></A> (r: RefReferent): RefHeader =
  BEGIN
    RETURN LOOPHOLE(r - ADRSIZE(Header), RefHeader);
  END HeaderOf;
</PRE> If a page is allocated, it can be normal or continued.  In the first
   case, there is a heap object just at the beginning of the page and
   others following.  The second case occurs when a heap object was too
   large to fit on a page: it starts at the beginning of a normal page and
   overflows on contiguous continued pages.  Whatever space is left on the
   last continued page is never used for another object or filler.  In
   other words, all the headers are on normal pages.
<P>
   Heap objects do not need to be adjacent.  Indeed, alignment constraints
   would make it difficult to ensure that property.  Filler objects may
   appear before objects to align them, or after the last object on a
   normal page to fill the page. 

<P> We need to be able to determine the size of an referent during
   collection; here is a functions to do just that.  It must be called with
   a non-nil pointer to the Header of a heap object that is there (has not
   been moved). 

<P><PRE>PROCEDURE <A NAME="ReferentSize"><procedure>ReferentSize</procedure></A> (h: RefHeader): CARDINAL =
  VAR
    res: INTEGER;
    tc: Typecode := h.typecode;
    def: TypeDefn;
  BEGIN
    IF tc = Fill_1_type THEN RETURN 0; END;
    IF tc = Fill_N_type THEN
      res := LOOPHOLE(h + ADRSIZE(Header), UNTRACED REF INTEGER)^;
      RETURN res - BYTESIZE(Header);
    END;
    def := RTType.Get (tc);
    IF def.nDimensions = 0 THEN
      (* the typecell datasize tells the truth *)
      RETURN def.dataSize;
    END;
    (* ELSE, the referent is an open array; it has the following layout:
|         pointer to the elements (ADDRESS)
|         size 1
|         ....
|         size n
|         optional padding
|         elements
|         ....
       where n is the number of open dimensions (given by the definition)
       and each size is the number of elements along the dimension *)
    VAR
      sizes: UNTRACED REF INTEGER := h + ADRSIZE(Header) + ADRSIZE(ADDRESS);
                                                           (* ^ elt pointer*)
    BEGIN
      res := 1;
      FOR i := 0 TO def.nDimensions - 1 DO
        res := res * sizes^;
        INC(sizes, ADRSIZE(sizes^));
      END;
      res := res * def.elementSize;
    END;
    res := RTMisc.Upper(res + def.dataSize, BYTESIZE(Header));
    RETURN res;
  END ReferentSize;
</PRE> The convention about page numbering allows for a simple conversion from
   an address to the number of the page in which it is, as well as from a
   page number to the first address is contains: 

<P><PRE>PROCEDURE <A NAME="ReferentToPage"><procedure>ReferentToPage</procedure></A> (r: RefReferent): Page =
  (* VAR p: INTEGER := LOOPHOLE(r, INTEGER) DIV BytesPerPage; *)
  VAR p: INTEGER := Word.RightShift (LOOPHOLE(r, INTEGER), LogBytesPerPage);
  BEGIN
    IF p &lt; p0 OR p &gt;= p1 OR desc[p - p0].space = Space.Unallocated
      THEN RETURN Nil;
      ELSE RETURN p;
    END;
  END ReferentToPage;

PROCEDURE <A NAME="HeaderToPage"><procedure>HeaderToPage</procedure></A> (r: RefHeader): Page =
  (* VAR p: INTEGER := LOOPHOLE(r, INTEGER) DIV BytesPerPage; *)
  VAR p: INTEGER := Word.RightShift (LOOPHOLE(r, INTEGER), LogBytesPerPage);
  BEGIN
    IF p &lt; p0 OR p &gt;= p1 OR desc[p - p0].space = Space.Unallocated
      THEN RETURN Nil;
      ELSE RETURN p;
    END;
  END HeaderToPage;

PROCEDURE <A NAME="PageToHeader"><procedure>PageToHeader</procedure></A> (p: Page): RefHeader =
  BEGIN
    RETURN LOOPHOLE(p * BytesPerPage, RefHeader);
  END PageToHeader;

PROCEDURE <A NAME="PageToAddress"><procedure>PageToAddress</procedure></A> (p: Page): ADDRESS =
  BEGIN
    RETURN LOOPHOLE(p * BytesPerPage, ADDRESS);
  END PageToAddress;
</PRE> We remember where we should look for free space with the following
   globals: 

<P><PRE>VAR
  newPtr, newBoundary: RefHeader;
  (* memory in [newPtr, newBoundary) is available to AllocForNew *)

  pureCopyPtr, pureCopyBoundary: RefHeader;
  (* memory in [pureCopyPtr, pureCopyBoundary) is available to AllocForCopy
     for pure objects (objects with no REFs) *)

  impureCopyPtr, impureCopyBoundary: RefHeader;
  (* memory in [impureCopyPtr, impureCopyBoundary) is available to
     AllocForCopy for impure objects (objects with REFs) *)
</PRE> To move a heap object to the new space, modifying the original
   reference to it 

<P><PRE>TYPE Mover = RTHeapMap.Visitor OBJECT OVERRIDES apply := Move END;

PROCEDURE <A NAME="Move"><procedure>Move</procedure></A> (&lt;*UNUSED*&gt; self: Mover;  cp: ADDRESS) =
  VAR
    refref := LOOPHOLE(cp, UNTRACED REF RefReferent);
    ref    := refref^;
  BEGIN
    IF ref = NIL THEN RETURN; END;
    VAR p := ReferentToPage(ref);
    BEGIN
      IF p = Nil THEN RETURN; END;
      VAR
        pi        := p - p0;
        oldHeader := HeaderOf(ref);
      BEGIN
        IF desc[pi].space # Space.Previous THEN
          RETURN;                (* nothing to do *)
        END;
        IF p + 1 &lt; p1 AND desc[pi + 1].continued THEN
          (* if this is a large object, just promote the pages *)
          VAR def := RTType.Get (oldHeader.typecode);
          BEGIN
            IF (def.gc_map = NIL) AND (def.parent = NIL) THEN
              PromotePage(
                p, Desc{space := Space.Current, generation := copyGeneration,
                        pure := TRUE, note := Note.Large, gray := FALSE,
                        protected := FALSE, continued := FALSE});
            ELSE
              PromotePage(
                p, Desc{space := Space.Current, generation := copyGeneration,
                        pure := FALSE, note := Note.Large, gray := TRUE,
                        protected := FALSE, continued := FALSE});
              desc[pi].link := impureCopyStack;
              impureCopyStack := p;
            END;
          END;
        ELSIF oldHeader.forwarded THEN
          (* if already moved, just update the reference *)
          refref^ := LOOPHOLE(ref, UNTRACED REF RefReferent)^;
        ELSE
          (* move the object *)
          VAR
            def      := RTType.Get(oldHeader.typecode);
            dataSize := ReferentSize(oldHeader);
            np       : RefReferent;
          BEGIN
            IF (def.gc_map # NIL) OR (def.parent # NIL) THEN
              np := AllocForImpureCopy(dataSize, def.dataAlignment);
            ELSE
              np := AllocForPureCopy(dataSize, def.dataAlignment);
            END;
            VAR newHeader := HeaderOf(np);
            BEGIN
              RTMisc.Copy(
                oldHeader, newHeader, BYTESIZE(Header) + dataSize);
            END;
            IF def.nDimensions # 0 THEN
              (* open array: update the internal pointer *)
              LOOPHOLE(np, UNTRACED REF ADDRESS)^ := np + def.dataSize;
            END;
            oldHeader.forwarded := TRUE;
            LOOPHOLE(ref, UNTRACED REF RefReferent)^ := np;
            refref^ := np;
          END;
        END;
      END;
    END;
  END Move;
</PRE> Determines whether a REF has yet been moved into the new space.  Follows
   the logic in <CODE>Move</CODE>.

<P><PRE>PROCEDURE <A NAME="Moved"><procedure>Moved</procedure></A> (ref: RefReferent): BOOLEAN =
  BEGIN
    IF ref = NIL THEN RETURN TRUE; END;
    (* check the space *)
    VAR p := ReferentToPage(ref);
    BEGIN
      IF p = Nil OR desc[p - p0].space # Space.Previous THEN
        RETURN TRUE;
      END;
    END;
    (* check the forwarded bit *)
    IF HeaderOf(LOOPHOLE(ref, ADDRESS)).forwarded THEN RETURN TRUE; END;
    (* not moved *)
    RETURN FALSE;
  END Moved;
</PRE> When an allocated page is referenced by the stack, we have to move it to
   the next space and insert it in the list of promoted pages.  In the case
   where the page is actually part of a group of pages for a big referent,
   we have to promote all these pages to the new space, but only the first
   one needs to be inserted in the queue, as it is the only one containing
   referent headers.
<P>
   This routine is passed to the Threads implementation.  It is called for
   each stack, where start and stop are the addresses of the first and last
   word of the stack under consideration. 

<P><PRE>PROCEDURE <A NAME="NoteStackLocations"><procedure>NoteStackLocations</procedure></A> (start, stop: ADDRESS) =
  VAR
    fp                                := start;
    firstAllocatedAddress             := PageToAddress(p0);
    firstNonAllocatedAddress          := PageToAddress(p1);
    p                       : ADDRESS;
    pp                      : Page;
  BEGIN
    WHILE fp &lt;= stop DO
      p := LOOPHOLE(fp, UNTRACED REF ADDRESS)^;
      IF firstAllocatedAddress &lt;= p AND p &lt; firstNonAllocatedAddress THEN
        pp := LOOPHOLE(p, INTEGER) DIV BytesPerPage;
        IF desc[pp - p0].space = Space.Previous THEN
          VAR fp := FirstPage(pp);
          BEGIN
            &lt;* ASSERT desc[fp - p0].space = Space.Previous *&gt;
            IF desc[fp - p0].pure THEN
              PromotePage(fp, Desc{space := Space.Current, pure := TRUE,
                                   note := Note.AmbiguousRoot, gray :=
                                   FALSE, generation := copyGeneration,
                                   protected := FALSE, continued := FALSE});
            ELSE
              PromotePage(fp, Desc{space := Space.Current, pure := FALSE,
                                   note := Note.AmbiguousRoot, gray :=
                                   TRUE, generation := copyGeneration,
                                   protected := FALSE, continued := FALSE});
              desc[fp - p0].link := impureCopyStack;
              impureCopyStack := fp;
            END;
          END;
        END;
      END;
      INC(fp, RTMachine.PointerAlignment);
    END;
  END NoteStackLocations;

PROCEDURE <A NAME="PromotePage"><procedure>PromotePage</procedure></A> (p: Page;  READONLY d: Desc) =
  BEGIN
    &lt;* ASSERT desc[p - p0].space = Space.Previous *&gt;
    &lt;* ASSERT NOT desc[p - p0].continued*&gt;
    VAR n := PageCount(p);
    BEGIN
      desc[p - p0] := d;
      IF n &gt; 1 THEN
        VAR dd := d;
        BEGIN
          dd.continued := TRUE;
          FOR pp := p + 1 TO p + n - 1 DO desc[pp - p0] := dd; END;
        END;
      END;
      IF perfOn THEN PerfChange(p, n); END;
      IF d.space = Space.Current THEN
        IF n = 1 THEN
          INC(smallPromotionPages, 1);
        ELSE
          INC(largePromotionPages, n);
        END;
      END;
    END;
  END PromotePage;

PROCEDURE <A NAME="InsertFiller"><procedure>InsertFiller</procedure></A> (start: RefHeader; n: INTEGER) =
  BEGIN
    IF n = 0 THEN
      (* nothing to do *)
    ELSIF n = ADRSIZE(Header) THEN
      start^ := FillHeader1;
    ELSIF n &gt;= ADRSIZE(Header) + ADRSIZE(INTEGER) THEN
      start^ := FillHeaderN;
      LOOPHOLE(start + ADRSIZE(Header), UNTRACED REF INTEGER)^ := n;
    ELSE
      &lt;* ASSERT FALSE *&gt;
    END;
  END InsertFiller;

TYPE CollectorState = {Zero, One, Two, Three, Four, Five};

VAR collectorState := CollectorState.Zero;

VAR
  threshold := ARRAY [0 .. 1] OF
                 REAL{FLOAT(InitialBytes DIV 4 DIV BytesPerPage - 1), 1.0};
  (* start a collection as soon as current space reaches threshold[0] /
     threshold[1] pages; the initial value is 64KB *)

  partialCollection: BOOLEAN;
  (* whether the collection in progress is partial, involving only the newer
     generation *)

  partialCollectionNext: BOOLEAN := FALSE;
  (* whether the next collection should be partial *)

  collectorOn: BOOLEAN := FALSE;

  copyGeneration: Generation := Generation.Younger;

  signalBackground := FALSE;
  (* should signal background collector thread? *)

  signalWeak := FALSE;
  (* should signal weak cleaner thread? *)

PROCEDURE <A NAME="CollectEnough"><procedure>CollectEnough</procedure></A> () =
  BEGIN
    IF collectorOn THEN RETURN; END;
    IF Behind() THEN
      CollectorOn();
      IF incremental AND RTHeapDep.VM AND disableVMCount = 0 THEN
        REPEAT CollectSome(); UNTIL NOT Behind();
      ELSE
        WHILE collectorState = CollectorState.Zero DO CollectSome(); END;
        REPEAT CollectSome(); UNTIL collectorState = CollectorState.Zero;
      END;
      CollectorOff();
    END;
  END CollectEnough;

PROCEDURE <A NAME="Behind"><procedure>Behind</procedure></A> (): BOOLEAN =
  BEGIN
    IF disableCount + disableMotionCount &gt; 0
         AND collectorState = CollectorState.Zero THEN
      RETURN FALSE;
    END;
    IF collectorState = CollectorState.Zero THEN
      RETURN FLOAT(smallCopyPages + largeCopyPages + smallPromotionPages
                     + largePromotionPages + smallNewPages + largeNewPages)
               * threshold[1] &gt;= threshold[0];
    ELSE
      RETURN FLOAT(smallNewPages + largeNewPages) * gcRatio
               &gt;= FLOAT(smallCopyPages + largeCopyPages);
    END;
  END Behind;

VAR timeUsedOnEntry: REAL;       (* time used when entered collector *)

PROCEDURE <A NAME="CollectorOn"><procedure>CollectorOn</procedure></A> () =
  BEGIN
    (* ASSERT locked *)
    &lt;* ASSERT NOT collectorOn *&gt;
    collectorOn := TRUE;

    IF incremental AND RTHeapDep.VM AND disableVMCount = 0 THEN
      (* The VM-synchronized collector doesn't worry about running threads. *)
    ELSE
      ThreadF.SuspendOthers ();
    END;

    IF RTHeapDep.VM THEN timeUsedOnEntry := RTHeapDep.TimeUsed(); END;
    IF impureCopyPage # Nil THEN
      &lt;* ASSERT desc[impureCopyPage - p0].gray *&gt;
      &lt;* ASSERT desc[impureCopyPage - p0].protected *&gt;
      Unprotect(impureCopyPage);
    END;
  END CollectorOn;

PROCEDURE <A NAME="CollectorOff"><procedure>CollectorOff</procedure></A> () =
  BEGIN
    (* ASSERT locked *)
    &lt;* ASSERT collectorOn *&gt;
    IF impureCopyPage # Nil THEN
      &lt;* ASSERT desc[impureCopyPage - p0].gray *&gt;
      &lt;* ASSERT NOT desc[impureCopyPage - p0].protected *&gt;
      Protect(impureCopyPage, readable := FALSE, writable := FALSE);
    END;
    VAR p := impureCopyStack;
    BEGIN
      WHILE p # Nil DO
        IF desc[p - p0].gray AND NOT desc[p - p0].protected THEN
          Protect(p, readable := FALSE, writable := FALSE);
        END;
        p := desc[p - p0].link;
      END;
    END;

    IF incremental AND RTHeapDep.VM AND disableVMCount = 0 THEN
      (* The VM-synchronized collector doesn't worry about running threads. *)
    ELSE
      ThreadF.ResumeOthers ();
    END;

    collectorOn := FALSE;
    IF signalBackground OR signalWeak THEN
      signalBackground := FALSE;
      signalWeak := FALSE;
      Broadcast();
    END;
    IF RTHeapDep.VM THEN
      cycleCost := cycleCost + (RTHeapDep.TimeUsed() - timeUsedOnEntry);
    END;
  END CollectorOff;

PROCEDURE <A NAME="CollectSome"><procedure>CollectSome</procedure></A> () =
  BEGIN
    &lt;* ASSERT disableCount = 0 *&gt;
    CASE collectorState OF
    | CollectorState.Zero =&gt; CollectSomeInStateZero();
    | CollectorState.One =&gt; CollectSomeInStateOne();
    | CollectorState.Two =&gt; CollectSomeInStateTwo();
    | CollectorState.Three =&gt; CollectSomeInStateThree();
    | CollectorState.Four =&gt; CollectSomeInStateFour();
    | CollectorState.Five =&gt; CollectSomeInStateFive();
    END;
  END CollectSome;
</PRE> Start a collection 

<P><PRE>VAR
  mover      : Mover    := NIL;
  cycleCost  : REAL     := 0.0;  (* running cost of current cycle *)
  cycleLength: CARDINAL := 1;    (* current planned cycle length *)
  cycleL     : CARDINAL := 0;    (* length of current cycle, so far *)
  cycleNews  : CARDINAL;         (* the number of new pages this cycle *)
  minPrefixAvgCost: REAL;        (* minimum average cost for a prefix of
                                    this cycle *)
  minCycleL: CARDINAL;           (* the length of that prefix *)

PROCEDURE <A NAME="CollectSomeInStateZero"><procedure>CollectSomeInStateZero</procedure></A> () =
  BEGIN
    &lt;* ASSERT disableCount + disableMotionCount = 0 *&gt;
    (* compute some costs relative to previous collection *)
    INC(cycleNews, smallNewPages + largeNewPages);
    VAR prefixAvgCost := cycleCost / FLOAT(cycleNews);
    BEGIN
      IF prefixAvgCost &lt; minPrefixAvgCost THEN
        minPrefixAvgCost := prefixAvgCost;
        minCycleL := cycleL;
      END;
    END;

    (* make generational decisions *)
    IF generational AND RTHeapDep.VM AND disableVMCount = 0 THEN
      copyGeneration := Generation.Older;
      partialCollection := partialCollectionNext AND cycleL &lt; cycleLength;
      IF NOT partialCollection THEN
        IF minCycleL = cycleLength THEN
          cycleLength := cycleLength + 1;
        ELSE
          cycleLength := MAX(cycleLength - 1, 1);
        END;
      END;
    ELSE
      copyGeneration := Generation.Younger;
      partialCollection := FALSE;
    END;
    partialCollectionNext := TRUE;

    IF partialCollection THEN
      INC(cycleL);
    ELSE
      cycleL := 1;
      cycleCost := 0.0;
      cycleNews := 0;
      minPrefixAvgCost := LAST(REAL);
      minCycleL := 0;
    END;

    InvokeMonitors (before := TRUE);

    IF perfOn THEN PerfBegin(); END;

    (* fill the rest of the current page *)
    InsertFiller(newPtr, newBoundary - newPtr);
    newPage := Nil;
    newStack := Nil;
    newPtr := NIL;
    newBoundary := NIL;

    INC(collections);

    (* flip spaces; newspace becomes oldspace *)
    FOR p := p0 TO p1 - 1 DO
      IF desc[p - p0].space = Space.Current THEN
        desc[p - p0].space := Space.Previous;
        IF perfOn THEN PerfChange(p, 1); END;
      END;
    END;

    IF perfOn THEN PerfFlip(); END;

    (* The 'new' nextSpace is empty *)
    smallNewPages := 0;
    largeNewPages := 0;
    smallCopyPages := 0;
    largeCopyPages := 0;
    smallPromotionPages := 0;
    largePromotionPages := 0;

    FOR p := p0 TO p1 - 1 DO
      IF desc[p - p0].space = Space.Previous
         AND NOT desc[p - p0].continued THEN
        IF desc[p - p0].generation = Generation.Older THEN
          IF partialCollection THEN
            &lt;* ASSERT copyGeneration = Generation.Older *&gt;
            IF desc[p - p0].protected THEN
              &lt;* ASSERT NOT desc[p - p0].pure *&gt;
              PromotePage(p, Desc{space := Space.Current, generation :=
                                  copyGeneration, pure := FALSE, note :=
                                  Note.OlderGeneration, gray := FALSE,
                                  protected := TRUE, continued := FALSE});
            ELSE
              IF desc[p - p0].pure THEN
                PromotePage(
                  p, Desc{space := Space.Current, generation :=
                          copyGeneration, pure := TRUE, note :=
                          Note.OlderGeneration, gray := FALSE, protected :=
                          FALSE, continued := FALSE});
              ELSE
                PromotePage(
                  p, Desc{space := Space.Current, generation :=
                          copyGeneration, pure := FALSE, note :=
                          Note.OlderGeneration, gray := TRUE, protected :=
                          FALSE, continued := FALSE});
                desc[p - p0].link := impureCopyStack;
                impureCopyStack := p;
              END;
            END;
          ELSE
            IF desc[p - p0].protected THEN Unprotect(p); END;
          END;
        ELSE
          &lt;* ASSERT NOT desc[p - p0].protected *&gt;
        END;
      END;
    END;
    (* now nothing in the previous space is protected or in the older
       generation *)

    mover := NEW (Mover);  (* get one in the new space *)

    (* mark from roots *)
    ThreadF.SuspendOthers();
    BEGIN
      (* Examine the stacks for possible pointers *)
      ThreadF.ProcessStacks(NoteStackLocations);

      (* Examine the global variables for possible pointers *)
      RTHeapMap.WalkGlobals (mover);
    END;
    ThreadF.ResumeOthers();

    IF perfOn THEN PerfPromotedRoots(); END;

    collectorState := CollectorState.One;
    IF backgroundWaiting THEN signalBackground := TRUE; END;
  END CollectSomeInStateZero;
</PRE> Clean gray nodes 

<P><PRE>PROCEDURE <A NAME="CollectSomeInStateOne"><procedure>CollectSomeInStateOne</procedure></A> () =
  BEGIN
    IF NOT CopySome() THEN collectorState := CollectorState.Two; END;
    IF backgroundWaiting THEN signalBackground := TRUE; END;
  END CollectSomeInStateOne;
</PRE> Walk weakly-referenced nodes to determine order in which to do cleanup,
   then cleanup gray nodes.  This should be broken down into parts, since
   it may be a lengthy operation. 

<P><PRE>PROCEDURE <A NAME="CollectSomeInStateTwo"><procedure>CollectSomeInStateTwo</procedure></A> () =
  BEGIN
    PreHandleWeakRefs();
    collectorState := CollectorState.Three;
    IF backgroundWaiting THEN signalBackground := TRUE; END;
  END CollectSomeInStateTwo;
</PRE> Clean gray nodes 

<P><PRE>PROCEDURE <A NAME="CollectSomeInStateThree"><procedure>CollectSomeInStateThree</procedure></A> () =
  BEGIN
    (* recursively copy all objects reachable from promoted objects.  marks
       &quot;marka&quot; and &quot;markb&quot; are cleared when objects move to the new
       space. *)
    IF NOT CopySome() THEN
      PostHandleWeakRefs();      (* must be called with no gray objects *)
      signalWeak := TRUE;
      collectorState := CollectorState.Four;
    END;
    IF backgroundWaiting THEN signalBackground := TRUE; END;
  END CollectSomeInStateThree;
</PRE> Clean gray nodes 

<P><PRE>PROCEDURE <A NAME="CollectSomeInStateFour"><procedure>CollectSomeInStateFour</procedure></A> () =
  BEGIN
    IF NOT CopySome() THEN collectorState := CollectorState.Five; END;
    IF backgroundWaiting THEN signalBackground := TRUE; END;
  END CollectSomeInStateFour;

PROCEDURE <A NAME="CollectSomeInStateFive"><procedure>CollectSomeInStateFive</procedure></A> () =
  BEGIN
    (* free all oldspace pages; oldspace becomes freespace *)
    FOR i := 0 TO p1 - p0 - 1 DO
      IF desc[i].space = Space.Previous THEN
        desc[i].space := Space.Free;
        desc[i].continued := FALSE;
        &lt;* ASSERT NOT desc[i].protected *&gt;
        IF perfOn THEN PerfChange(p0 + i, 1); END;
      END;
    END;

    RebuildFreelist();

    (* fill the rest of the current copy pages *)
    InsertFiller(pureCopyPtr, pureCopyBoundary - pureCopyPtr);
    InsertFiller(impureCopyPtr, impureCopyBoundary - impureCopyPtr);
    IF impureCopyPage # Nil THEN
      desc[impureCopyPage - p0].gray := FALSE;
      IF perfOn THEN PerfChange(impureCopyPage, 1); END;
      IF desc[impureCopyPage - p0].generation = Generation.Older THEN
        &lt;* ASSERT desc[impureCopyPage - p0].space = Space.Current *&gt;
        Protect(impureCopyPage, readable := TRUE, writable := FALSE);
      END;
      impureCopyPage := Nil;
    END;
    &lt;* ASSERT impureCopyStack = Nil *&gt;
    pureCopyPage := Nil;
    pureCopyStack := Nil;
    impureCopyPtr := NIL;
    impureCopyBoundary := NIL;
    pureCopyPtr := NIL;
    pureCopyBoundary := NIL;

    IF perfOn THEN PerfEnd(); END;

    InvokeMonitors(before := FALSE);

    IF partialCollection THEN
      IF FLOAT(smallCopyPages + largeCopyPages + smallPromotionPages
                 + largePromotionPages) * threshold[1] &gt;= threshold[0] THEN
        partialCollectionNext := FALSE;
      ELSE
        partialCollectionNext := TRUE;
      END;
    ELSE
      threshold[0] :=
        FLOAT(smallCopyPages + largeCopyPages + smallPromotionPages
                + largePromotionPages) * (gcRatio + 1.0);
      threshold[1] := gcRatio;
      partialCollectionNext := TRUE;
    END;

    collectorState := CollectorState.Zero;
  END CollectSomeInStateFive;
</PRE> CopySome attempts to make progress toward cleaning the new space.  It
   returns FALSE iff there was no more work to do.
<P>
   It operates by cleaning the current copy page.  It may also clean some
   number of pages on the stack.  When it returns, there is a new copy
   page. 

<P><PRE>PROCEDURE <A NAME="CopySome"><procedure>CopySome</procedure></A> (): BOOLEAN =
  VAR
    originalImpureCopyPage := impureCopyPage;
    originalBoundary       := impureCopyBoundary;
    cleanTo                := PageToHeader(impureCopyPage);
  BEGIN
    LOOP
      IF cleanTo &lt; impureCopyPtr THEN
        VAR ptr := impureCopyPtr;
        BEGIN
          CleanBetween(cleanTo, ptr);
          cleanTo := ptr;
        END;
      ELSE
        IF impureCopyStack = Nil THEN RETURN FALSE; END;
        VAR p := impureCopyStack;
        BEGIN
          impureCopyStack := desc[p - p0].link;
          &lt;* ASSERT NOT desc[p - p0].pure *&gt;
          IF desc[p - p0].gray THEN
            IF desc[p - p0].protected THEN Unprotect(p); END;
            CleanBetween(PageToHeader(p), PageToHeader(p + 1));
            FOR i := 0 TO PageCount(p) - 1 DO
              desc[p + i - p0].gray := FALSE;
            END;
            IF desc[p - p0].generation = Generation.Older THEN
              &lt;* ASSERT desc[p - p0].space = Space.Current *&gt;
              Protect(p, readable := TRUE, writable := FALSE);
            END;
            IF perfOn THEN PerfChange(p, PageCount(p)); END;
          END;
        END;
      END;
      IF impureCopyPage # originalImpureCopyPage THEN EXIT; END;
    END;
    CleanBetween(cleanTo, originalBoundary);
    (* originalImpureCopyPage is now in the stack; mark it not gray *)
    IF originalImpureCopyPage # Nil THEN
      desc[originalImpureCopyPage - p0].gray := FALSE;
      IF desc[originalImpureCopyPage - p0].generation = Generation.Older THEN
        &lt;* ASSERT desc[originalImpureCopyPage - p0].space = Space.Current *&gt;
        Protect(
          originalImpureCopyPage, readable := TRUE, writable := FALSE);
      END;
      IF perfOn THEN PerfChange(originalImpureCopyPage, 1); END;
    END;
    RETURN TRUE;
  END CopySome;

PROCEDURE <A NAME="CleanBetween"><procedure>CleanBetween</procedure></A> (h, he: RefHeader) =
  BEGIN
    WHILE h &lt; he DO
      &lt;* ASSERT Word.And (LOOPHOLE (h, INTEGER), 3) = 0 *&gt;
      &lt;* ASSERT NOT h.forwarded *&gt;
      h.marka := FALSE;
      h.markb := FALSE;
      RTHeapMap.WalkRef (h, mover);
      INC(h, ADRSIZE(Header) + ReferentSize(h));
    END;
  END CleanBetween;
</PRE> We maintain a list in weakTable, starting at weakLive0, of weak refs and
   the objects they reference.  This table is not considered a root.  When
   HandleWeakRefs is entered, any object mentioned in that list is a
   candidate for cleanup.
<P>
   First, we determine which weakly-referenced objects with non-NIL
   cleanups (<CODE>WRNNC objects</CODE>) are reachable from other WRNNC objects, by
   walking the old space.  All such WRNNC objects are copied to new space,
   and all the objects they reference.
<P>
   All the weakly-referenced objects left in the old space can then be
   scheduled for cleanup; we move them from the list starting at weakLive0
   to the list starting at weakDead0 in weakTable.  A separate thread runs
   WeakCleaner, which does the calls to the procedures.
<P>
   Note that the refs in weakTable must be updated to point to new
   space. 

<P> PreHandleWeakRefs walks the weakly-references structures in old-space,
   deciding on a cleanup order. 

<P><PRE>PROCEDURE <A NAME="PreHandleWeakRefs"><procedure>PreHandleWeakRefs</procedure></A> () =
  VAR s: Stacker;
  BEGIN
    (* get ready to allocate on a new page (take this out!) *)
    InsertFiller(impureCopyPtr, impureCopyBoundary - impureCopyPtr);
    InsertFiller(pureCopyPtr, pureCopyBoundary - pureCopyPtr);
    (* allocate a stack on the side for walking the old space *)
    s := InitStack();
    (* iterate over the weak refs to walk the old space *)
    VAR i := weakLive0;
    BEGIN
      WHILE i # -1 DO
        (* here, all old-space WRNNC objects that have already been scanned
           have marka set, as do all old-space objects reachable from them;
           all old-space WRNNC objects that were reachable from other
           already-scanned WRNNC objects have been promoted to the new
           space. *)
        WITH entry = weakTable[i] DO
          IF entry.p # NIL AND NOT Moved(entry.r) THEN
            (* we haven't seen this WRNNC object before *)
            VAR header := HeaderOf(LOOPHOLE(entry.r, ADDRESS));
            BEGIN
              IF NOT header.marka THEN
                &lt;* ASSERT NOT header.markb *&gt;
                (* visit all old-space objects reachable from it; promote
                   all other old-space WRNNC objects reachable from it;
                   promote all old-space objects reachable from it that
                   have &quot;marka&quot; set.  mark all visited nodes with
                   &quot;markb&quot;. *)
                WeakWalk1(s, entry.r);
                &lt;* ASSERT NOT header.marka *&gt;
                &lt;* ASSERT header.markb *&gt;
                (* then change all &quot;markb&quot; to &quot;marka&quot; *)
                WeakWalk2(s, entry.r);
                &lt;* ASSERT header.marka *&gt;
                &lt;* ASSERT NOT header.markb *&gt;
              END;
            END;
          END;
          i := entry.next;
        END;
      END;
    END;
  END PreHandleWeakRefs;
</PRE> WeakWalk1 starts at a WRNNC object and visits all objects in old space
   reachable from it, using <CODE>markb</CODE> to keep from visiting them more than
   once.  All other WRNNC objects visited are promoted, as are all objects
   already visited from other WRNNC objects. 

<P><PRE>PROCEDURE <A NAME="WeakWalk1"><procedure>WeakWalk1</procedure></A> (s: Stacker; ref: RefReferent) =
  VAR ref0 := ref;
  BEGIN
    &lt;* ASSERT s.empty() *&gt;
    LOOP
      IF NOT Moved(ref) THEN
        VAR header := HeaderOf(ref);
        BEGIN
          IF header.marka THEN
            &lt;* ASSERT NOT header.markb *&gt;
            Move(NIL, ADR(ref));
          ELSIF NOT header.markb THEN
            IF header.weak AND ref # ref0 THEN
              Move(NIL, ADR(ref));
            ELSE
              header.markb := TRUE;
              RTHeapMap.WalkRef (header, s);
            END;
          END;
        END;
      END;
      IF s.empty() THEN EXIT; END;
      ref := s.pop();
    END;
  END WeakWalk1;
</PRE> WeakWalk2 starts at a WRNNC objects and visits all objects in the old
   space that are reachable from it, changing <CODE>markb</CODE> to <CODE>marka</CODE> 

<P><PRE>PROCEDURE <A NAME="WeakWalk2"><procedure>WeakWalk2</procedure></A> (s: Stacker;  ref: RefReferent) =
  BEGIN
    &lt;* ASSERT s.empty() *&gt;
    LOOP
      IF NOT Moved(ref) THEN
        VAR header := HeaderOf(ref);
        BEGIN
          IF header.markb THEN
            header.markb := FALSE;
            header.marka := TRUE;
            RTHeapMap.WalkRef (header, s);
          END;
        END;
      END;
      IF s.empty() THEN EXIT; END;
      ref := s.pop();
    END;
  END WeakWalk2;

PROCEDURE <A NAME="PostHandleWeakRefs"><procedure>PostHandleWeakRefs</procedure></A> () =
  BEGIN
    (* move to a new page (take this out!) *)
    InsertFiller(impureCopyPtr, impureCopyBoundary - impureCopyPtr);
    InsertFiller(pureCopyPtr, pureCopyBoundary - pureCopyPtr);
    (* iterate over all weak refs.  if the object hasn't been promoted,
       schedule a cleanup *)
    VAR
      i        := weakLive0;
      previous := -1;
    BEGIN
      WHILE i # -1 DO
        WITH entry = weakTable[i] DO
          IF Moved(entry.r) THEN
            (* no cleanup this time; note new address *)
            Move(NIL, ADR(entry.r));
            previous := i;
            i := entry.next;
          ELSE
            (* the weak ref is dead; there are no cleanups *)
            VAR header := HeaderOf(LOOPHOLE(entry.r, ADDRESS));
            BEGIN
              header.weak := FALSE;
            END;
            (* move the entry from the weakLive0 list into the weakDead0 or
               weakFree0 list *)
            VAR next := entry.next;
            BEGIN
              IF previous = -1 THEN
                weakLive0 := next;
              ELSE
                weakTable[previous].next := next;
              END;
              entry.t.a := -1;   (* keep ToRef from succeeding *)
              IF entry.p # NIL THEN
                entry.next := weakDead0;
                weakDead0 := i;
              ELSE
                entry.next := weakFree0;
                weakFree0 := i;
              END;
              i := next;
            END;
          END;
        END;
      END;
    END;
    (* for all entries on the weakDead0 list, including those just placed
       there, note the new address *)
    VAR i := weakDead0;
    BEGIN
      WHILE i # -1 DO
        WITH entry = weakTable[i] DO
          &lt;* ASSERT entry.t.a = -1 *&gt;
          Move(NIL, ADR(entry.r));
          i := entry.next;
        END;
      END;
    END;
    (* finally, check for objects with final cleanup enabled *)
    VAR
      i        := weakFinal0;
      previous := -1;
    BEGIN
      WHILE i # -1 DO
        WITH entry = weakTable[i] DO
          IF Moved(entry.r) THEN
            (* no cleanup this time; note new address *)
            Move(NIL, ADR(entry.r));
            previous := i;
            i := entry.next;
          ELSE
            (* call the cleanup procedure *)
            LOOPHOLE(entry.p, PROCEDURE (p: REFANY))(
              LOOPHOLE(entry.r, REFANY));
            (* take the entry off the weakFinal0 list and put it on the
               weakFree0 list; on to the next entry *)
            VAR next := entry.next;
            BEGIN
              IF previous = -1 THEN
                weakFinal0 := next;
              ELSE
                weakTable[previous].next := next;
              END;
              entry.next := weakFree0;
              weakFree0 := i;
              i := next;
            END;
          END;
        END;
      END;
    END;
  END PostHandleWeakRefs;
</PRE> The stack for walking the old space is maintained on the heap in the new
   space. 

<P><PRE>TYPE
  Stacker = RTHeapMap.Visitor OBJECT
    data : REF ARRAY OF RefReferent;
    x0   : UNTRACED REF RefReferent;
    x1   : UNTRACED REF RefReferent;
    xA   : UNTRACED REF RefReferent;
    xN   : CARDINAL;
  METHODS
    empty (): BOOLEAN     := StackEmpty;
    pop   (): RefReferent := PopStack;
  OVERRIDES
    apply := PushStack;
  END;
</PRE> InitStack allocates an initial stack of 100 elements. 

<P><PRE>PROCEDURE <A NAME="InitStack"><procedure>InitStack</procedure></A> (): Stacker =
  VAR s := NEW (Stacker);
  BEGIN
    s.data := NEW(REF ARRAY OF RefReferent, 100);
    s.xN   := NUMBER (s.data^);
    s.x0   := ADR(s.data[0]);
    s.x1   := s.x0 + s.xN * ADRSIZE(RefReferent);
    s.xA   := s.x0;
    RETURN s;
  END InitStack;
</PRE> PushStack pushes an object onto the stack, growing it if necessary. 

<P><PRE>PROCEDURE <A NAME="PushStack"><procedure>PushStack</procedure></A> (s: Stacker;  cp: ADDRESS) =
  VAR ref: RefReferent := LOOPHOLE(cp, UNTRACED REF RefReferent)^;
  BEGIN
    IF ref # NIL THEN
      IF s.xA = s.x1 THEN ExpandStack (s); END;
      s.xA^ := ref;
      INC(s.xA, ADRSIZE(RefReferent));
    END;
  END PushStack;

PROCEDURE <A NAME="ExpandStack"><procedure>ExpandStack</procedure></A> (s: Stacker) =
  VAR
    newStackN := 2 * s.xN;
    newStack: REF ARRAY OF RefReferent := NEW(REF ARRAY OF RefReferent,
                                                  newStackN);
  BEGIN
    SUBARRAY(newStack^, 0, s.xN) := SUBARRAY(s.data^, 0, s.xN);
    s.x0   := ADR(newStack^[0]);
    s.xA   := s.x0 + s.xN * ADRSIZE(RefReferent);
    s.x1   := s.x0 + newStackN * ADRSIZE(RefReferent);
    s.data := newStack;
    s.xN   := newStackN;
  END ExpandStack;
</PRE> PopStack pops an object off the stack. 

<P><PRE>PROCEDURE <A NAME="PopStack"><procedure>PopStack</procedure></A> (s: Stacker): RefReferent =
  BEGIN
    DEC(s.xA, ADRSIZE(RefReferent));
    RETURN s.xA^;
  END PopStack;
</PRE> StackEmpty tells if the stack is empty. 

<P><PRE>PROCEDURE <A NAME="StackEmpty"><procedure>StackEmpty</procedure></A> (s: Stacker): BOOLEAN =
  BEGIN
    RETURN s.xA = s.x0;
  END StackEmpty;
</PRE> Malloc returns the address of <CODE>size</CODE> bytes of untraced, zeroed storage 

<P><PRE>PROCEDURE <A NAME="Malloc"><procedure>Malloc</procedure></A> (size: INTEGER): ADDRESS =
  VAR res: ADDRESS;
  BEGIN
    RTOS.LockHeap();
    BEGIN
      res := Cstdlib.malloc(size);
      IF res = NIL THEN
        RTMisc.FatalError(NIL, 0, &quot;malloc failed, unable to get more memory&quot;);
      END;
    END;
    RTOS.UnlockHeap();
    RTMisc.Zero(res, size);
    RETURN res;
  END Malloc;
</PRE> AllocForNew allocates space for a NEW. 

<P><PRE>PROCEDURE <A NAME="AllocForNew"><procedure>AllocForNew</procedure></A> (dataSize, dataAlignment: CARDINAL): RefReferent =
  VAR
    alignment : INTEGER;
    nextNewPtr: RefHeader;
  BEGIN
    RTOS.LockHeap();
    (* Where would this heap object end if we were to allocate at
       newPtr? *)
    VAR referentTry := newPtr + ADRSIZE(Header);
    BEGIN
      (* ---------------- see CheckTypes ---------------------------------
|      WITH a = RTMisc.Align (referentTry, dataAlignment) DO
|        alignment := a - referentTry;
|        nextNewPtr := LOOPHOLE (a + dataSize, RefHeader); END;
         ------------------------------------------------------------------ *)
      alignment :=
        align[Word.And(LOOPHOLE(referentTry, INTEGER), MaxAlignMask),
              dataAlignment];
      nextNewPtr := referentTry + (alignment + dataSize);
    END;
    (* If this is not ok, take the long route *)
    IF nextNewPtr &gt; newBoundary THEN
      nextNewPtr := NIL;         (* clear in case of GC *)
      VAR
        res := LongAlloc(dataSize, dataAlignment, currentPtr := newPtr,
                         currentBoundary := newBoundary,
                         currentPage := newPage, stack := newStack,
                         allocMode := AllocMode.New, pure := FALSE);
      BEGIN
        RTOS.UnlockHeap();
        RETURN res;
      END;
    END;
    (* Align the referent *)
    IF alignment # 0 THEN
      InsertFiller(newPtr, alignment);
      newPtr := newPtr + alignment;
    END;
    VAR res := LOOPHOLE(newPtr + ADRSIZE(Header), RefReferent);
    BEGIN
      newPtr := nextNewPtr;
      RTOS.UnlockHeap();
      RETURN res;
    END;
  END AllocForNew;
</PRE> AllocForPureCopy and AllocForImpureCopy allocate space to copy an object
   from oldspace; they have the same logic as AllocForNew. 

<P><PRE>PROCEDURE <A NAME="AllocForPureCopy"><procedure>AllocForPureCopy</procedure></A> (dataSize, dataAlignment: CARDINAL):
  RefReferent =
  VAR
    alignment       : INTEGER;
    nextPureCopyPtr : RefHeader;
    res, referentTry: RefReferent;
  BEGIN
    &lt;* ASSERT collectorOn *&gt;
    RTOS.LockHeap();
    (* Where would this heap object end if we were to allocate at
       pureCopyPtr? *)
    referentTry := pureCopyPtr + ADRSIZE(Header);

    (* ---------------- see CheckTypes ---------------------------------
|    WITH a = RTMisc.Align (referentTry, dataAlignment) DO
|      alignment := a - referentTry;
|      nextPureCopyPtr := LOOPHOLE (a + dataSize, RefHeader); END;
       ------------------------------------------------------------------ *)
    VAR tmp := Word.And(LOOPHOLE(referentTry, INTEGER), MaxAlignMask);
    BEGIN
      alignment := align[tmp, dataAlignment];
      nextPureCopyPtr := referentTry + (alignment + dataSize);
    END;

    (* If this is not ok, take the long route *)
    IF nextPureCopyPtr &gt; pureCopyBoundary THEN
      res := LongAlloc(dataSize, dataAlignment, currentPtr := pureCopyPtr,
                       currentBoundary := pureCopyBoundary,
                       currentPage := pureCopyPage, stack := pureCopyStack,
                       allocMode := AllocMode.Copy, pure := TRUE);
      RTOS.UnlockHeap();
      RETURN res;
    END;

    (* Align the referent *)
    IF alignment # 0 THEN
      InsertFiller(pureCopyPtr, alignment);
      pureCopyPtr := pureCopyPtr + alignment;
    END;

    res := LOOPHOLE(pureCopyPtr + ADRSIZE(Header), RefReferent);
    pureCopyPtr := nextPureCopyPtr;
    RTOS.UnlockHeap();
    RETURN res;
  END AllocForPureCopy;

PROCEDURE <A NAME="AllocForImpureCopy"><procedure>AllocForImpureCopy</procedure></A> (dataSize, dataAlignment: CARDINAL):
  RefReferent =
  VAR
    alignment        : INTEGER;
    nextImpureCopyPtr: RefHeader;
    res, referentTry : RefReferent;
  BEGIN
    &lt;* ASSERT collectorOn *&gt;
    RTOS.LockHeap();
    (* Where would this heap object end if we were to allocate at
       ImpureCopyPtr? *)
    referentTry := impureCopyPtr + ADRSIZE(Header);

    (* ---------------- see CheckTypes ---------------------------------
|    WITH a = RTMisc.Align (referentTry, dataAlignment) DO
|      alignment := a - referentTry;
|      nextImpureCopyPtr := LOOPHOLE (a + dataSize, RefHeader); END;
       ------------------------------------------------------------------ *)
    VAR tmp := Word.And(LOOPHOLE(referentTry, INTEGER), MaxAlignMask);
    BEGIN
      alignment := align[tmp, dataAlignment];
      nextImpureCopyPtr := referentTry + (alignment + dataSize);
    END;

    (* If this is not ok, take the long route *)
    IF nextImpureCopyPtr &gt; impureCopyBoundary THEN
      res :=
        LongAlloc(dataSize, dataAlignment, currentPtr := impureCopyPtr,
                  currentBoundary := impureCopyBoundary,
                  currentPage := impureCopyPage, stack := impureCopyStack,
                  allocMode := AllocMode.Copy, pure := FALSE);
      RTOS.UnlockHeap();
      RETURN res;
    END;

    (* Align the referent *)
    IF alignment # 0 THEN
      InsertFiller(impureCopyPtr, alignment);
      impureCopyPtr := impureCopyPtr + alignment;
    END;

    res := LOOPHOLE(impureCopyPtr + ADRSIZE(Header), RefReferent);
    impureCopyPtr := nextImpureCopyPtr;
    RTOS.UnlockHeap();
    RETURN res;
  END AllocForImpureCopy;

TYPE AllocMode = {New, Copy};

PROCEDURE <A NAME="LongAlloc"><procedure>LongAlloc</procedure></A> (              dataSize, dataAlignment    : CARDINAL;
                     VAR (*INOUT*) currentPtr, currentBoundary: RefHeader;
                     VAR (*INOUT*) currentPage                : Page;
                     VAR (*INOUT*) stack                      : Page;
                                   allocMode                  : AllocMode;
                                   pure                       : BOOLEAN    ):
  RefReferent =
  VAR
    nbBytes := RTMisc.Upper(ADRSIZE(Header), dataAlignment) + dataSize;
    nbPages := (nbBytes + AdrPerPage - 1) DIV AdrPerPage;
    res     : RefReferent;
    notAfter: SET OF Note;
  BEGIN
    IF allocMode = AllocMode.New THEN
      CollectEnough();
      notAfter := SET OF Note{Note.Copied};
    ELSE
      notAfter := SET OF Note{Note.Allocated};
    END;

    VAR
      thisPage := FindFreePages(nbPages, notAfter := notAfter);
      (* thisPage points to a block of nbPages contiguous, free pages; just
         what we need! *)
      thisPtr                  := PageToHeader(thisPage);
      thisBoundary             := PageToHeader(thisPage + 1);
      gray                     := allocMode = AllocMode.Copy AND NOT pure;
      generation  : Generation;
      note        : Note;
    BEGIN
      (* maybe we have to put a filler to align this thing *)
      res := RTMisc.Align(thisPtr + ADRSIZE(Header), dataAlignment);
      InsertFiller(thisPtr, res - ADRSIZE(Header) - thisPtr);

      (* allocate the object *)
      thisPtr := LOOPHOLE(res + dataSize, RefHeader);

      IF allocMode = AllocMode.New THEN
        generation := Generation.Younger;
        note := Note.Allocated;
      ELSE
        generation := copyGeneration;
        IF generation = Generation.Older THEN &lt;* ASSERT gray OR pure *&gt;
        END;
        note := Note.Copied;
      END;
      desc[thisPage - p0] :=
        Desc{space := Space.Current, generation := generation, pure :=
             pure, note := note, gray := gray, protected := FALSE,
             continued := FALSE};
      IF nbPages = 1 THEN
        CASE allocMode OF
        | AllocMode.New =&gt; INC(smallNewPages);
        | AllocMode.Copy =&gt; INC(smallCopyPages);
        END;
      ELSE
        CASE allocMode OF
        | AllocMode.New =&gt; INC(largeNewPages, nbPages);
        | AllocMode.Copy =&gt; INC(largeCopyPages, nbPages);
        END;
        FOR i := 1 TO nbPages - 1 DO
          desc[thisPage + i - p0] :=
            Desc{space := Space.Current, generation := generation, pure :=
                 pure, note := note, gray := gray, protected := FALSE,
                 continued := TRUE};
        END;
      END;
      IF perfOn THEN PerfChange(thisPage, nbPages); END;

      IF nbPages = 1 THEN
        IF thisBoundary - thisPtr &gt; currentBoundary - currentPtr THEN
          (* more allocation space available on this page; fill and file
             the current page *)
          InsertFiller(currentPtr, currentBoundary - currentPtr);
          IF currentPage # Nil THEN
            &lt;* ASSERT desc[currentPage - p0].space = Space.Current *&gt;
            desc[currentPage - p0].link := stack;
            stack := currentPage;
            IF allocMode = AllocMode.Copy THEN
              &lt;* ASSERT desc[currentPage - p0].gray
                   OR desc[currentPage - p0].pure *&gt;
            END;
          END;
          currentPtr := thisPtr;
          currentBoundary := thisBoundary;
          currentPage := thisPage;
        ELSE
          (* more allocation space available on current page; fill and file
             this page *)
          InsertFiller(thisPtr, thisBoundary - thisPtr);
          desc[thisPage - p0].link := stack;
          stack := thisPage;
        END;
      ELSE
        (* file this page *)
        desc[thisPage - p0].link := stack;
        stack := thisPage;
      END;
    END;
    RETURN res;
  END LongAlloc;
</PRE>--------------------------------------------------

<P><PRE>VAR
  backgroundWaiting   := FALSE;
</PRE> The background thread may be present or not.  If it is present, it
   speeds collection asynchronously.  Because it makes progress slowly, it
   should impose only a small overhead when the mutator is running, but
   quickly complete a collection if the collector pauses. 

<P><PRE>PROCEDURE <A NAME="BackgroundThread"><procedure>BackgroundThread</procedure></A> (&lt;* UNUSED *&gt; closure: Thread.Closure): REFANY =
  BEGIN
    LOOP
      backgroundWaiting := TRUE; (* no locks, unfortunately *)
      WHILE collectorState = CollectorState.Zero DO Wait(); END;
      backgroundWaiting := FALSE;
      WHILE collectorState # CollectorState.Zero DO
        RTOS.LockHeap();
        BEGIN
          IF collectorState # CollectorState.Zero THEN
            CollectorOn();
            CollectSome();
            CollectorOff();
          END;
        END;
        RTOS.UnlockHeap();
        Thread.Pause(1.0d0);       (* one second *)
      END;
    END;
  END BackgroundThread;
</PRE> --------------------------------------------------------- collector 

<P><PRE>PROCEDURE <A NAME="StartGC"><procedure>StartGC</procedure></A> () =
  BEGIN
    StartCollection();
  END StartGC;

PROCEDURE <A NAME="FinishGC"><procedure>FinishGC</procedure></A> () =
  BEGIN
    FinishCollection();
  END FinishGC;

PROCEDURE <A NAME="Crash"><procedure>Crash</procedure></A> (): BOOLEAN =
  VAR result: BOOLEAN;
  BEGIN
    RTOS.LockHeap();        (* left incremented *)
    IF collectorState = CollectorState.Zero THEN
      (* no collection in progress *)
      collectorOn := TRUE;       (* left on *)
      result := TRUE;
    ELSIF NOT collectorOn THEN
      CollectorOn();             (* left on *)
      (* finish collection *)
      WHILE collectorState # CollectorState.Zero DO CollectSome(); END;
      result := TRUE;
    ELSE
      collectorOn := TRUE;       (* left on *)
      result := FALSE;
    END;
    (* unprotect all pages *)
    FOR p := p0 TO p1 - 1 DO
      IF desc[p - p0].protected THEN Unprotect(p); END;
    END;
    RETURN result;
  END Crash;
</PRE> --------------------------------------------------------- debugging 

<P><PRE>VAR
  protectedCheck, refCheck: RTHeapMap.Visitor;

PROCEDURE <A NAME="InstallSanityCheck"><procedure>InstallSanityCheck</procedure></A> () =
  BEGIN
    RegisterMonitor(
      NEW(MonitorClosure, before := SanityCheck, after := SanityCheck));
    IF (refCheck = NIL) THEN
      protectedCheck := NEW (RTHeapMap.Visitor,
                             apply := ProtectedOlderRefSanityCheck);
      refCheck := NEW (RTHeapMap.Visitor, apply := RefSanityCheck);
    END;
  END InstallSanityCheck;
</PRE> SanityCheck checks the heap for correctness when no collection is in
   progress. 

<P><PRE>PROCEDURE <A NAME="SanityCheck"><procedure>SanityCheck</procedure></A> (&lt;*UNUSED*&gt; self: MonitorClosure) =
  VAR p := p0;
  BEGIN
    WHILE p &lt; p1 DO
      CASE desc[p - p0].space OF
      | Space.Unallocated =&gt; INC(p);
      | Space.Previous =&gt;        &lt;* ASSERT FALSE *&gt;
      | Space.Current =&gt;
          &lt;* ASSERT NOT desc[p - p0].gray *&gt;
          &lt;* ASSERT NOT desc[p - p0].continued *&gt;
          IF desc[p - p0].protected THEN
            &lt;* ASSERT desc[p - p0].generation = Generation.Older *&gt;
          END;
          (* visit the objects on the page *)
          VAR
            h  := PageToHeader(p);
            he := PageToHeader(p + 1);
          BEGIN
            IF p = newPage THEN he := newPtr; END;
            WHILE h &lt; he DO
              (* check the references in the object *)
              IF desc[p - p0].protected THEN
                RTHeapMap.WalkRef (h, protectedCheck);
              ELSE
                RTHeapMap.WalkRef (h, refCheck);
              END;
              INC(h, ADRSIZE(Header) + ReferentSize(h));
            END;
            IF h &gt; he THEN
              &lt;* ASSERT HeaderToPage(h - 1) = p + PageCount(p) - 1 *&gt;
            ELSE
              &lt;* ASSERT PageCount(p) = 1 *&gt;
            END;
          END;
          VAR
            n := PageCount(p);
            d := desc[p - p0];
          BEGIN
            d.continued := TRUE;
            d.link := Nil;
            LOOP
              INC(p);
              DEC(n);
              IF n = 0 THEN EXIT; END;
              VAR dd := desc[p - p0];
              BEGIN
                dd.link := Nil;
                &lt;* ASSERT dd = d *&gt;
              END;
            END;
          END;
      | Space.Free =&gt;
          &lt;* ASSERT NOT desc[p - p0].continued *&gt;
          INC(p);
      END;
    END;
    &lt;* ASSERT p = p1 *&gt;
  END SanityCheck;

PROCEDURE <A NAME="RefSanityCheck"><procedure>RefSanityCheck</procedure></A> (&lt;*UNUSED*&gt;v: RTHeapMap.Visitor;  cp  : ADDRESS) =
  VAR ref := LOOPHOLE(cp, REF RefReferent)^;
  BEGIN
    IF ref # NIL THEN
      VAR
        p  := ReferentToPage(ref);
        h  := HeaderOf(ref);
        tc := h.typecode;
      BEGIN
        IF p0 &lt;= p AND p &lt; p1 THEN
          &lt;* ASSERT desc[p - p0].space = Space.Current *&gt;
          &lt;* ASSERT NOT desc[p - p0].continued *&gt;
          &lt;* ASSERT (0 &lt; tc AND tc &lt; RT0u.nTypes)
                      OR tc = Fill_1_type
                      OR tc = Fill_N_type *&gt;
        ELSE
          (* the compiler generates Text.T that are not in the traced
             heap *)
          &lt;* ASSERT tc = 1 *&gt;
        END;
      END;
    END;
  END RefSanityCheck;

PROCEDURE <A NAME="ProtectedOlderRefSanityCheck"><procedure>ProtectedOlderRefSanityCheck</procedure></A> (&lt;*UNUSED*&gt; v  : RTHeapMap.Visitor;
                                                   cp : ADDRESS) =
  VAR ref := LOOPHOLE(cp, REF RefReferent)^;
  BEGIN
    IF ref # NIL THEN
      VAR
        p  := ReferentToPage(ref);
        h  := HeaderOf(ref);
        tc := h.typecode;
      BEGIN
        IF p0 &lt;= p AND p &lt; p1 THEN
          &lt;* ASSERT desc[p - p0].space = Space.Current *&gt;
          &lt;* ASSERT desc[p - p0].generation = Generation.Older *&gt;
          &lt;* ASSERT NOT desc[p - p0].continued *&gt;
          &lt;* ASSERT (0 &lt; tc AND tc &lt; RT0u.nTypes)
                      OR tc = Fill_1_type
                      OR tc = Fill_N_type *&gt;
        ELSE
          (* the compiler generates Text.T that are not in the traced
             heap *)
          &lt;* ASSERT tc = 1 *&gt;
        END;
      END;
    END;
  END ProtectedOlderRefSanityCheck;
</PRE> ----------------------------------------------------------------------- 

<P><PRE>PROCEDURE <A NAME="VisitAllRefs"><procedure>VisitAllRefs</procedure></A> (v: RefVisitor) =
  VAR tc: Typecode;
  BEGIN
    TRY
      Disable();
      FOR p := p0 TO p1 - 1 DO
        IF desc[p - p0].space = Space.Current
             AND NOT desc[p - p0].continued THEN
          VAR
            h             := PageToHeader(p);
            he            := PageToHeader(p + 1);
            size: INTEGER;
          BEGIN
            WHILE h &lt; he AND (p # newPage OR h &lt; newPtr) DO
              size := ReferentSize(h);
              tc := h.typecode;
              IF tc # Fill_1_type AND tc # Fill_N_type THEN
                IF NOT v.visit(
                         tc, LOOPHOLE(h + ADRSIZE(Header), REFANY), size) THEN
                  RETURN;
                END;
              END;
              INC(h, ADRSIZE(Header) + size);
            END;
          END;
        END;
      END;
    FINALLY
      Enable();
    END;
  END VisitAllRefs;

TYPE
  CountClosure = MonitorClosure OBJECT
                   tcs    : REF ARRAY OF Typecode;
                   counts : REF ARRAY OF CARDINAL;
                   visitor: RefVisitor;
                 OVERRIDES
                   after := CountRefsForTypecodes;
                 END;

TYPE
  CountAllClosure = MonitorClosure OBJECT
                      counts : REF ARRAY OF CARDINAL;
                      visitor: RefVisitor;
                    OVERRIDES
                      after := CountRefsForAllTypecodes;
                    END;

TYPE
  CountVisitor =
    RefVisitor OBJECT cl: CountClosure OVERRIDES visit := One; END;

  CountAllVisitor =
    RefVisitor OBJECT cl: CountAllClosure OVERRIDES visit := All; END;

PROCEDURE <A NAME="One"><procedure>One</procedure></A> (           self: CountVisitor;
                          tc  : Typecode;
               &lt;*UNUSED*&gt; r   : REFANY;
               &lt;*UNUSED*&gt; size: CARDINAL      ): BOOLEAN =
  BEGIN
    FOR i := FIRST(self.cl.tcs^) TO LAST(self.cl.tcs^) DO
      IF self.cl.tcs[i] = tc THEN INC(self.cl.counts[i]); RETURN TRUE; END;
    END;
    RETURN TRUE;
  END One;

PROCEDURE <A NAME="All"><procedure>All</procedure></A> (           self: CountAllVisitor;
                          tc  : Typecode;
               &lt;*UNUSED*&gt; r   : REFANY;
               &lt;*UNUSED*&gt; size: CARDINAL         ): BOOLEAN =
  BEGIN
    INC(self.cl.counts[tc]);
    RETURN TRUE;
  END All;

PROCEDURE <A NAME="CountRefsForTypecodes"><procedure>CountRefsForTypecodes</procedure></A> (cl: CountClosure) =
  BEGIN
    FOR i := FIRST(cl.counts^) TO LAST(cl.counts^) DO
      cl.counts[i] := 0;
    END;
    VisitAllRefs(cl.visitor);
    FOR i := FIRST(cl.tcs^) TO LAST(cl.tcs^) DO
      PutText(&quot;count[&quot;);
      PutInt(cl.tcs[i]);
      PutText(&quot;] = &quot;);
      PutInt(cl.counts[i]);
      IF i # LAST(cl.tcs^) THEN PutText(&quot;,  &quot;); END;
    END;
    PutText(&quot;\n&quot;);
  END CountRefsForTypecodes;

PROCEDURE <A NAME="CountRefsForAllTypecodes"><procedure>CountRefsForAllTypecodes</procedure></A> (cl: CountAllClosure) =
  BEGIN
    FOR i := FIRST(cl.counts^) TO LAST(cl.counts^) DO
      cl.counts[i] := 0;
    END;
    VisitAllRefs(cl.visitor);
    FOR i := FIRST(cl.counts^) TO LAST(cl.counts^) DO
      IF cl.counts[i] &gt; 1 THEN
        PutInt(i);
        PutText(&quot;: &quot;);
        PutInt(cl.counts[i]);
        IF i # LAST(cl.counts^) THEN PutText(&quot;, &quot;); END;
      END;
    END;
    PutText(&quot;\n&quot;);
  END CountRefsForAllTypecodes;
</PRE> ---------------------------------------------------- showheap hooks 

<P><PRE>VAR
  perfW  : RTPerfTool.Handle;
  perfOn : BOOLEAN := FALSE;

CONST
  EventSize = (BITSIZE(RTHeapEvent.T) + BITSIZE(CHAR) - 1) DIV BITSIZE(CHAR);

PROCEDURE <A NAME="PerfStart"><procedure>PerfStart</procedure></A> () =
  VAR i, j: Page;
  BEGIN
    IF RTPerfTool.Start(&quot;showheap&quot;, perfW) THEN
      perfOn := TRUE;
      RTProcess.RegisterExitor(PerfStop);
      PerfGrow(p0, p1 - p0);

      i := p0;
      WHILE i # Nil AND i &lt; p1 DO
        j := i + 1;
        WHILE j &lt; p1 AND desc[j - p0].continued DO INC(j); END;
        IF desc[i - p0].space # Space.Free THEN PerfChange(i, j - i); END;
        i := j;
      END;
    END;
  END PerfStart;

PROCEDURE <A NAME="PerfFlip"><procedure>PerfFlip</procedure></A> () =
  VAR e := RTHeapEvent.T{kind := RTHeapEvent.Kind.Flip};
  BEGIN
    perfOn := RTPerfTool.Send (perfW, ADR(e), EventSize);
  END PerfFlip;

PROCEDURE <A NAME="PerfPromotedRoots"><procedure>PerfPromotedRoots</procedure></A> () =
  VAR e := RTHeapEvent.T{kind := RTHeapEvent.Kind.Roots};
  BEGIN
    perfOn := RTPerfTool.Send (perfW, ADR(e), EventSize);
  END PerfPromotedRoots;

PROCEDURE <A NAME="PerfStop"><procedure>PerfStop</procedure></A> () =
  VAR e := RTHeapEvent.T{kind := RTHeapEvent.Kind.Bye};
  BEGIN
    (* UNSAFE, but needed to prevent deadlock if we're crashing! *)
    EVAL RTPerfTool.Send (perfW, ADR(e), EventSize);
    RTPerfTool.Close (perfW);
  END PerfStop;

PROCEDURE <A NAME="PerfAllow"><procedure>PerfAllow</procedure></A> (&lt;*UNUSED*&gt; n: INTEGER := 0) =
  VAR
    e := RTHeapEvent.T{kind := RTHeapEvent.Kind.Off, nb :=
                       disableCount + disableMotionCount};
  BEGIN
    perfOn := RTPerfTool.Send (perfW, ADR(e), EventSize);
  END PerfAllow;

PROCEDURE <A NAME="PerfBegin"><procedure>PerfBegin</procedure></A> () =
  VAR e := RTHeapEvent.T{kind := RTHeapEvent.Kind.Begin};
  BEGIN
    perfOn := RTPerfTool.Send (perfW, ADR(e), EventSize);
  END PerfBegin;

PROCEDURE <A NAME="PerfEnd"><procedure>PerfEnd</procedure></A> () =
  VAR e := RTHeapEvent.T{kind := RTHeapEvent.Kind.End};
  BEGIN
    perfOn := RTPerfTool.Send (perfW, ADR(e), EventSize);
  END PerfEnd;

PROCEDURE <A NAME="PerfChange"><procedure>PerfChange</procedure></A> (first: Page; nb: CARDINAL) =
  VAR
    e := RTHeapEvent.T{kind := RTHeapEvent.Kind.Change, first := first,
                       nb := nb, desc := desc[first - p0]};
  BEGIN
    perfOn := RTPerfTool.Send (perfW, ADR(e), EventSize);
  END PerfChange;

PROCEDURE <A NAME="PerfGrow"><procedure>PerfGrow</procedure></A> (firstNew: Page; nb: CARDINAL) =
  VAR
    e := RTHeapEvent.T{
           kind := RTHeapEvent.Kind.Grow, first := firstNew, nb := nb};
  BEGIN
    perfOn := RTPerfTool.Send (perfW, ADR(e), EventSize);
  END PerfGrow;
</PRE> ----------------------------------------------------------------------- 

<P> RTWeakRef 

<P> weakTable contains four singly-linked lists: for entries in use (rooted
   at index weakLive0), entries with final cleanup (at weakFinal0), dead
   entries awaiting cleanup (at weakDead0), and free entries (at
   weakFree0).
<P>
   Entries in use contain the weak ref, the REF, and the procedure.  The
   <CODE>a</CODE> field of the weak ref is the index in the table; this speeds lookup.
   The <CODE>b</CODE> field is a unique value, taken from a 32-bit counter.
<P>
   Dead entries contain the same dields, but the <CODE>a</CODE> field of the weak ref
   is set to -1 to keep lookups from succeeding.  When the cleanup
   procedure is to be called, the original weak ref can still be
   reconstructed, since the <CODE>a</CODE> field was the index. 

<P><PRE>VAR
  weakTable: UNTRACED REF ARRAY OF WeakEntry; (* allocated in &quot;Init&quot; *)
             (* := NEW(UNTRACED REF ARRAY OF WeakEntry, 0); *)
  weakLive0  := -1;              (* the root of the in-use list *)
  weakFinal0 := -1;              (* the root of the thread-cleanup list *)
  weakDead0  := -1;              (* the root of the dead list *)
  weakFree0  := -1;              (* the root of the free list *)

TYPE
  Int32 = BITS 32 FOR [-16_7fffffff-1 .. 16_7fffffff];
  WeakRefAB = RECORD
                a: Int32;
                b: Int32;
              END;
  WeakEntry = RECORD
                t: WeakRefAB;    (* the weak ref, if well-formed *)
                r: RefReferent;  (* the traced reference *)
                p: ADDRESS;      (* a WeakRefCleanUpProc or a PROCEDURE(r:
                                    REFANY) *)
                next: INTEGER;   (* the next entry on the list *)
              END;
</PRE> This is WeakRef.FromRef, which returns a new weak ref for an object. 

<P><PRE>VAR startedWeakCleaner := FALSE;

PROCEDURE <A NAME="WeakRefFromRef"><procedure>WeakRefFromRef</procedure></A> (r: REFANY; p: WeakRefCleanUpProc := NIL): WeakRef =
  VAR
    start           := FALSE;
    result: WeakRef;
  BEGIN
    &lt;* ASSERT r # NIL *&gt;
    RTOS.LockHeap();
    BEGIN
      (* create a WeakCleaner thread the first time through *)
      IF p # NIL AND NOT startedWeakCleaner THEN
        start := TRUE;
        startedWeakCleaner := TRUE;
      END;
      (* if necessary, expand weakTable *)
      IF weakFree0 = -1 THEN ExpandWeakTable(); END;
      IF p # NIL THEN
        (* mark the object as having a weak ref with non-nil cleanup *)
        VAR header := HeaderOf(LOOPHOLE(r, ADDRESS));
        BEGIN
          &lt;* ASSERT NOT header^.weak *&gt;
          header^.weak := TRUE;
        END;
      END;
      (* allocate a new entry *)
      VAR i := weakFree0;
      BEGIN
        weakFree0 := weakTable[i].next;
        (* generate a new weak ref *)
        VAR t := WeakRefAB{a := i, b := Word.Plus(weakTable[i].t.b, 1)};
        BEGIN
          &lt;* ASSERT t.b # 0 *&gt;
          (* set up the entry *)
          weakTable[i] :=
            WeakEntry{t := t, r := LOOPHOLE(r, RefReferent), p :=
                      LOOPHOLE(p, ADDRESS), next := weakLive0};
          weakLive0 := i;
          result := LOOPHOLE(t, WeakRef);
        END;
      END;
    END;
    RTOS.UnlockHeap();
    IF start THEN
      EVAL Thread.Fork(NEW(Thread.Closure, apply := WeakCleaner));
    END;
    RETURN result;
  END WeakRefFromRef;

PROCEDURE <A NAME="ExpandWeakTable"><procedure>ExpandWeakTable</procedure></A> () =
  VAR
    newTable := NEW(UNTRACED REF ARRAY OF WeakEntry,
                    2 * NUMBER(weakTable^) + 1);
  BEGIN
    SUBARRAY(newTable^, 0, NUMBER(weakTable^)) := weakTable^;
    FOR i := NUMBER(weakTable^) TO NUMBER(newTable^) - 1 DO
      WITH entry = newTable[i] DO
        entry.t.b := 0;
        entry.next := weakFree0;
        weakFree0 := i;
      END;
    END;
    weakTable := newTable;
  END ExpandWeakTable;
</PRE> This is WeakRef.ToRef, which inverts FromRef 

<P><PRE>PROCEDURE <A NAME="WeakRefToRef"><procedure>WeakRefToRef</procedure></A> (READONLY t: WeakRef): REFANY =
  VAR ab: WeakRefAB;  r: REFANY := NIL;
  BEGIN
    LOOPHOLE (ab, WeakRef) := t;
    RTOS.LockHeap();
    (* if the weak ref is not dead, we know the index *)
    WITH entry = weakTable[ab.a] DO
      (* check the weak ref there *)
      IF entry.t = ab THEN
        &lt;* ASSERT entry.r # NIL *&gt;
        IF collectorState # CollectorState.Zero THEN
          VAR p := ReferentToPage(entry.r);
          BEGIN
            &lt;* ASSERT p # Nil *&gt;
            IF desc[p - p0].space = Space.Previous THEN
              CollectorOn();
              Move(NIL, ADR(entry.r));
              CollectorOff();
            END;
          END;
        END;
        r := LOOPHOLE(entry.r, REFANY);
      END;
    END;
    RTOS.UnlockHeap();
    RETURN r;
  END WeakRefToRef;
</PRE> This is RTHeapRef.RegisterFinalCleanup, which registers final cleanup
   for a heap object. 

<P><PRE>PROCEDURE <A NAME="RegisterFinalCleanup"><procedure>RegisterFinalCleanup</procedure></A> (r: REFANY; p: PROCEDURE (r: REFANY)) =
  BEGIN
    &lt;* ASSERT r # NIL *&gt;
    &lt;* ASSERT p # NIL *&gt;
    RTOS.LockHeap();
    BEGIN
      (* if necessary, expand weakTable *)
      IF weakFree0 = -1 THEN ExpandWeakTable(); END;
      (* allocate a new entry *)
      VAR i := weakFree0;
      BEGIN
        weakFree0 := weakTable[i].next;
        (* set up the entry, without a weak ref *)
        weakTable[i].r := LOOPHOLE(r, RefReferent);
        weakTable[i].p := LOOPHOLE(p, ADDRESS);
        weakTable[i].next := weakFinal0;
        weakFinal0 := i;
      END;
    END;
    RTOS.UnlockHeap();
  END RegisterFinalCleanup;
</PRE> WeakCleaner waits for entries to be placed on the dead list, then cleans
   them up and puts them on the free list. 

<P><PRE>PROCEDURE <A NAME="WeakCleaner"><procedure>WeakCleaner</procedure></A> (&lt;*UNUSED*&gt; closure: Thread.Closure): REFANY =
  VAR
    i   : INTEGER;
    copy: WeakEntry;
  BEGIN
    LOOP
      (* get an entry to handle.  copy its contents, then put it on the
         free list. *)
      WHILE weakDead0 = -1 DO Wait(); END;
      RTOS.LockHeap();
      IF weakDead0 = -1 THEN
        RTOS.UnlockHeap();
      ELSE
        i := weakDead0;
        WITH entry = weakTable[i] DO
          &lt;* ASSERT entry.t.a = -1 *&gt;
          CollectorOn();
          Move(NIL, ADR(entry.r));
          CollectorOff();
          copy := entry;
          weakDead0 := entry.next;
          entry.next := weakFree0;
          weakFree0 := i;
        END;
        RTOS.UnlockHeap();
        (* call the registered procedure.  note that collections are
           allowed; the copy is kept on the stack so the object won't be
           freed during the call. *)
        IF copy.p # NIL THEN
          LOOPHOLE(copy.p, WeakRefCleanUpProc)(
            LOOPHOLE(WeakRefAB{a := i, b := copy.t.b}, WeakRef),
            LOOPHOLE(copy.r, REFANY));
        END;
        copy.r := NIL;           (* to help conservative collector *)
      END;
    END;
  END WeakCleaner;
</PRE> ----------------------------------------------------------------------- 

<P><PRE>PROCEDURE <A NAME="FirstPage"><procedure>FirstPage</procedure></A> (p: Page): Page =
  BEGIN
    WHILE desc[p - p0].continued DO DEC(p); END;
    RETURN p;
  END FirstPage;

PROCEDURE <A NAME="PageCount"><procedure>PageCount</procedure></A> (p: Page): CARDINAL =
  VAR n := 0;
  BEGIN
    &lt;* ASSERT NOT desc[p - p0].continued *&gt;
    REPEAT INC(p); INC(n); UNTIL p &gt;= p1 OR NOT desc[p - p0].continued;
    RETURN n;
  END PageCount;
</PRE> ----------------------------------------------------------------------- 

<P><PRE>PROCEDURE <A NAME="Protect"><procedure>Protect</procedure></A> (p: Page; readable, writable: BOOLEAN) =
  VAR n := PageCount(p);
  BEGIN
    &lt;* ASSERT collectorOn OR (readable AND writable) *&gt;
    &lt;* ASSERT RTHeapDep.VM *&gt;
    &lt;* NOWARN *&gt;&lt;* ASSERT TRUE *&gt;
    RTHeapDep.Protect(p, n, readable, writable);
    VAR protected := NOT (readable AND writable);
    BEGIN
      FOR i := 0 TO n - 1 DO desc[p + i - p0].protected := protected; END;
    END;
    IF perfOn THEN PerfChange(p, n); END;
  END Protect;

PROCEDURE <A NAME="Unprotect"><procedure>Unprotect</procedure></A> (p: Page) =
  BEGIN
    Protect(p, readable := TRUE, writable := TRUE);
  END Unprotect;

PROCEDURE <A NAME="Fault"><procedure>Fault</procedure></A> (addr: ADDRESS): BOOLEAN =
  VAR p := LOOPHOLE(addr, INTEGER) DIV BytesPerPage;
  BEGIN
    ThreadF.SuspendOthers();
    &lt;* ASSERT RTHeapDep.VM *&gt;
    &lt;* NOWARN *&gt;&lt;* ASSERT TRUE *&gt;
    cycleCost :=
      cycleCost + RTHeapDep.VMFaultTime(); (* otherwise unaccounted for *)
    IF NOT (p0 &lt;= p AND p &lt; p1) OR desc[p - p0].space = Space.Unallocated THEN
      ThreadF.ResumeOthers();
      RETURN FALSE;              (* not in heap *)
    END;
    IF NOT desc[p - p0].protected THEN
      ThreadF.ResumeOthers();
      RETURN TRUE;               (* was protected, but not any more *)
    END;
    &lt;* ASSERT NOT desc[p - p0].pure *&gt;
    IF desc[p - p0].gray THEN
      CollectorOn();
      IF p = impureCopyPage THEN
        IF CopySome() THEN
          &lt;* ASSERT NOT desc[p - p0].gray *&gt;
        ELSE
          IF desc[p - p0].gray THEN
            &lt;* ASSERT p = impureCopyPage AND impureCopyStack = Nil *&gt;
            InsertFiller(impureCopyPtr, impureCopyBoundary - impureCopyPtr);
            impureCopyPage := Nil;
            impureCopyStack := Nil;
            impureCopyPtr := NIL;
            impureCopyBoundary := NIL;
            FOR i := 0 TO PageCount(p) - 1 DO
              desc[p + i - p0].gray := FALSE;
            END;
            IF desc[p - p0].generation = Generation.Older THEN
              &lt;* ASSERT desc[p - p0].space = Space.Current *&gt;
              Protect(p, readable := TRUE, writable := FALSE);
            END;
            IF perfOn THEN PerfChange(p, 1); END;
          END;
        END;
      ELSE
        p := FirstPage(p);
        Unprotect(p);
        CleanBetween(PageToHeader(p), PageToHeader(p + 1));
        FOR i := 0 TO PageCount(p) - 1 DO
          desc[p + i - p0].gray := FALSE;
        END;
        IF desc[p - p0].generation = Generation.Older THEN
          &lt;* ASSERT desc[p - p0].space = Space.Current *&gt;
          Protect(p, readable := TRUE, writable := FALSE);
        END;
        IF perfOn THEN PerfChange(p, PageCount(p)); END;
      END;
      CollectorOff();
    ELSE
      p := FirstPage(p);
      &lt;* ASSERT desc[p - p0].generation = Generation.Older *&gt;
      Unprotect(p);
    END;
    ThreadF.ResumeOthers();
    RETURN TRUE;                 (* was protected, protection cleared *)
  END Fault;
</PRE> ----------------------------------------------------------------------- 

<P> The inner-loop collector action is to pick a gray page and completely
   clean it (i.e., make its referents at least gray, so that the page
   becomes black).  The current gray page, <CODE>impureCopyPage</CODE> is
   distinguished; it's the page that newly gray objects are copied to.
<P>
   To improve locality of referene in the new space, we keep the set of
   gray pages as a stack.  This helps approximate a depth-first copy to
   newspace.  The current page is not a member of the stack, but will
   become one when it becomes full.  The current page is always the page
   that contains newPtr.
<P>
   To reduce page faults, we separate the <CODE>pure</CODE> copy pages (those whose
   objects contain no REFs) from the <CODE>impure</CODE> ones (those with REFs).  Only
   impure pages become gray, since pure pages can have no REFs into the old
   space (since they have no REFs at all). 

<P><PRE>VAR
  impureCopyPage: Page := Nil;   (* the current impure copy page *)
  impureCopyStack: Page := Nil;  (* threaded through the &quot;link&quot; field; ends
                                    at Nil *)
</PRE> By analogy, we also maintain <CODE>pureCopyPage</CODE> and <CODE>pureCopyStack</CODE>.  These
   are not used, but maintaining them simplifies the code. 

<P><PRE>VAR
  pureCopyPage: Page;            (* the current pure copy page *)
  pureCopyStack: Page;           (* threaded through the &quot;link&quot; field; ends
                                    at Nil *)
</PRE> By analogy, we also maintain <CODE>newPage</CODE> and <CODE>newStack</CODE>.  As with
   pureCopyPage and pureCopyStack, these are not used, but maintaining them
   simplifies the code. 

<P><PRE>VAR
  newPage: Page;                 (* the current new page *)
  newStack: Page;                (* threaded through the &quot;link&quot; field; ends
                                    at Nil *)
</PRE> Minimum number of bytes by which to grow the heap.  Setting it higher
   reduces the number of system calls; setting it lower keeps the heap a
   little smaller. 

<P> ----------------------------------------------------------------------- 

<P>***** Page-level allocator *****

<P> The freelist is sorted by blocksize, linked through the first page in
   each block, using the <CODE>link</CODE> field in the <CODE>desc</CODE> array.  Page allocation
   is best-fit.  For elements of the same blocksize, they are sorted by
   page number, to make the showheap display more easily readable, and to
   slightly reduce fragmentation. 

<P> FindFreePages allocates a run of <CODE>n</CODE> free pages, which we would prefer
   not be near pages in the current space with notes in notAfter.  The
   allocator can thus be used to separate pages with different notes, since
   they will have different lifetimes.  This is a concern only when
   incremental and generational collection are combined. 

<P><PRE>PROCEDURE <A NAME="FindFreePages"><procedure>FindFreePages</procedure></A> (n: INTEGER; notAfter: SET OF Note): Page =
  VAR p: Page;
  BEGIN
    IF collectorState = CollectorState.Zero THEN
      p := AllocateFreePagesFromBlock(n, SET OF Note{}, TRUE);
      IF p # Nil THEN RETURN p; END;
    ELSE
      p := AllocateFreePagesFromBlock(n, notAfter, TRUE);
      IF p # Nil THEN RETURN p; END;
      p := AllocateFreePagesFromBlock(n, SET OF Note{}, FALSE);
      IF p # Nil THEN RETURN p; END;
    END;
    GrowHeap(n);
    p := AllocateFreePagesFromBlock(n, SET OF Note{}, TRUE);
    &lt;* ASSERT p # Nil *&gt;
    RETURN p;
  END FindFreePages;

VAR free: Page;                  (* the head of the freelist *)
</PRE> AllocateFreePagesFromBlock finds the first block large enough to satisfy
   the request.  <CODE>notAfter</CODE> is the set of page notes in the current space
   that the block allocated from must not immediately follow; this is used
   to separate Note.Allocated pages from Note.Copied pages.  If <CODE>front</CODE> is
   TRUE, the pages will be allocated from the beginning of the block, else
   from the end; this is also used to separate Note.Allocated Pages from
   Note.Copied pages.  If the block is bigger than the request, the
   remainder is left at the right point in the freelist.  If no block
   exists, Nil is returned. 

<P><PRE>PROCEDURE <A NAME="AllocateFreePagesFromBlock"><procedure>AllocateFreePagesFromBlock</procedure></A> (n       : INTEGER;
                                      notAfter: SET OF Note;
                                      front   : BOOLEAN      ): Page =
  VAR
    p                   := free;
    prevP               := Nil;
    prevLength          := 0;
    length    : INTEGER;
  BEGIN
    LOOP
      IF p = Nil THEN RETURN Nil; END;
      length := FreeLength(p);
      IF length &gt;= n
           AND NOT (p &gt; p0 AND desc[(p - 1) - p0].space = Space.Current
                      AND desc[(p - 1) - p0].note IN notAfter) THEN
        EXIT;
      END;
      prevP := p;
      prevLength := length;
      p := desc[p - p0].link;
    END;
    IF length = n THEN
      IF prevP = Nil THEN
        free := desc[p - p0].link;
      ELSE
        desc[prevP - p0].link := desc[p - p0].link;
      END;
      RETURN p;
    ELSE
      VAR
        newP, fragP: Page;
        fragLength : CARDINAL;
      BEGIN
        IF front THEN
          newP := p;
          fragP := p + n;
        ELSE
          newP := p + length - n;
          fragP := p;
        END;
        fragLength := length - n;
        IF fragLength &gt; prevLength THEN
          IF prevP = Nil THEN
            free := fragP;
          ELSE
            desc[prevP - p0].link := fragP;
          END;
          desc[fragP - p0].link := desc[p - p0].link;
        ELSE
          IF prevP = Nil THEN
            free := desc[p - p0].link;
          ELSE
            desc[prevP - p0].link := desc[p - p0].link;
          END;
          VAR
            pp     := free;
            prevPP := Nil;
          BEGIN
            LOOP
              IF pp = Nil THEN EXIT; END;
              VAR length := FreeLength(pp);
              BEGIN
                IF length &gt; fragLength
                     OR (length = fragLength AND pp &gt; fragP) THEN
                  EXIT;
                END;
              END;
              prevPP := pp;
              pp := desc[pp - p0].link;
            END;
            desc[fragP - p0].link := pp;
            IF prevPP = Nil THEN
              free := fragP;
            ELSE
              desc[prevPP - p0].link := fragP;
            END;
          END;
        END;
        RETURN newP;
      END;
    END;
  END AllocateFreePagesFromBlock;
</PRE> RebuildFreelist rebuilds the free list, from the <CODE>desc</CODE> array.  It first
   links all free blocks into the free list, then it sorts the free list.
   The sort used is insertion sort, which is quadratic in the number of
   different block sizes, but only linear in the number of pages. 

<P><PRE>PROCEDURE <A NAME="RebuildFreelist"><procedure>RebuildFreelist</procedure></A> () =
  BEGIN
    VAR
      prevP     := Nil;
      prevSpace := Space.Unallocated;
    BEGIN
      (* link together the first pages of all free blocks *)
      FOR p := p0 TO p1 - 1 DO
        VAR space := desc[p - p0].space;
        BEGIN
          IF space = Space.Free AND prevSpace # Space.Free THEN
            IF prevP = Nil THEN
              free := p;
            ELSE
              desc[prevP - p0].link := p;
            END;
            prevP := p;
          END;
          prevSpace := space;
        END;
      END;
      IF prevP = Nil THEN
        free := Nil;
      ELSE
        desc[prevP - p0].link := Nil;
      END;
    END;
    (* sort them, using insertion sort *)
    VAR
      n     := 1;                (* smallest block size *)
      p     := free;             (* start of sublist we're examining *)
      prevP := Nil;              (* element before sublist *)
    BEGIN
      LOOP
        VAR
          excess     := Nil;
          prevExcess := Nil;
        BEGIN
          (* separate off blocks over &quot;n&quot; long into excess list *)
          WHILE p # Nil DO
            VAR length := FreeLength(p);
            BEGIN
              &lt;* ASSERT length &gt;= n *&gt;
              IF length &gt; n THEN
                IF prevExcess = Nil THEN
                  excess := p;
                ELSE
                  desc[prevExcess - p0].link := p;
                END;
                IF prevP = Nil THEN
                  free := desc[p - p0].link;
                ELSE
                  desc[prevP - p0].link := desc[p - p0].link;
                END;
                prevExcess := p;
              ELSE
                prevP := p;
              END;
            END;
            p := desc[p - p0].link;
          END;
          (* maybe done *)
          IF excess = Nil THEN EXIT; END;
          &lt;* ASSERT prevExcess # Nil *&gt;
          (* link longer blocks onto end *)
          IF prevP = Nil THEN
            free := excess;
          ELSE
            desc[prevP - p0].link := excess;
          END;
          desc[prevExcess - p0].link := Nil;
          p := excess;
        END;
        (* find smallest element size of remaining bocks *)
        n := LAST(CARDINAL);
        VAR pp := p;
        BEGIN
          REPEAT
            VAR length := FreeLength(pp);
            BEGIN
              IF length &lt; n THEN n := length; END;
            END;
            pp := desc[pp - p0].link;
          UNTIL pp = Nil;
        END;
      END;
    END;
  END RebuildFreelist;
</PRE> FreeLength returns the number of free pages starting at page p. 

<P><PRE>PROCEDURE <A NAME="FreeLength"><procedure>FreeLength</procedure></A> (p: Page): CARDINAL =
  BEGIN
    &lt;* ASSERT desc[p - p0].space = Space.Free *&gt;
    VAR pp := p + 1;
    BEGIN
      LOOP
        IF pp &gt;= p1 THEN EXIT; END;
        IF desc[pp - p0].space # Space.Free THEN EXIT; END;
        INC(pp);
      END;
      RETURN pp - p;
    END;
  END FreeLength;
</PRE> GrowHeap adds a block of at least <CODE>minNewPages</CODE> free pages to the heap,
   and links it into the free list. 

<P><PRE>VAR fragment0, fragment1: ADDRESS := NIL;

CONST
  InitialBytes = 262144;         (* initial heap size is 256K *)
  MinNewBytes  = 262144;         (* grow the heap by at least 256K *)
  MinNewFactor = 0.2;            (* grow the heap by at least 20% *)

PROCEDURE <A NAME="GrowHeap"><procedure>GrowHeap</procedure></A> (pp: INTEGER) =
  VAR
    newChunk    : ADDRESS;
    newSideSpan : INTEGER;
    firstNewPage: Page;
    lastNewPage : Page;
    newP0       : Page;
    newP1       : Page;
  BEGIN
    IF allocatedPages = 0 THEN
      pp := MAX(pp, (InitialBytes + BytesPerPage - 1) DIV BytesPerPage);
    ELSE
      pp := MAX(pp, (MinNewBytes + BytesPerPage - 1) DIV BytesPerPage);
      pp := MAX(pp, CEILING(FLOAT(allocatedPages) * MinNewFactor));
    END;
    VAR bytes := (pp + 1) * BytesPerPage;
    BEGIN
      newChunk := RTOS.GetMemory(bytes);
      IF newChunk = NIL OR newChunk = LOOPHOLE(-1, ADDRESS) THEN
        RTMisc.FatalError(NIL, 0, &quot;gc: Could not extend the traced heap&quot;);
      END;
      IF fragment1 = newChunk THEN
        newChunk := fragment0;
        bytes := bytes + (fragment1 - fragment0);
      END;
      VAR excess := (-LOOPHOLE(newChunk, INTEGER)) MOD BytesPerPage;
      BEGIN
        INC(newChunk, excess);
        DEC(bytes, excess);
      END;
      VAR pages := bytes DIV BytesPerPage;
      BEGIN
        firstNewPage := LOOPHOLE(newChunk, INTEGER) DIV BytesPerPage;
        lastNewPage := firstNewPage + pages - 1;
        fragment0 :=
          LOOPHOLE((firstNewPage + pages) * BytesPerPage, ADDRESS);
        fragment1 := newChunk + bytes;
      END;
    END;
    (* determine the new boundaries of the allocated pages *)
    IF p0 = Nil THEN
      newP0 := firstNewPage;
      newP1 := lastNewPage + 1;
    ELSIF firstNewPage &lt; p0 THEN
      newP0 := firstNewPage;
      newP1 := p1;
    ELSIF p1 &lt;= lastNewPage THEN
      newP0 := p0;
      newP1 := lastNewPage + 1;
    ELSE
      newP0 := p0;
      newP1 := p1;
    END;
    (* extend the side arrays if necessary *)
    newSideSpan := newP1 - newP0;
    IF desc = NIL OR newSideSpan # NUMBER(desc^) THEN
      WITH newDesc = NEW(UNTRACED REF ARRAY OF Desc, newSideSpan) DO
        IF desc # NIL THEN
          FOR i := FIRST(desc^) TO LAST(desc^) DO
            newDesc[i + p0 - newP0] := desc[i];
          END;
          FOR i := p1 TO firstNewPage - 1 DO
            newDesc[i - newP0].space := Space.Unallocated;
          END;
          FOR i := lastNewPage + 1 TO p0 - 1 DO
            newDesc[i - newP0].space := Space.Unallocated;
          END;
          DISPOSE(desc);
        END;
        desc := newDesc;
      END;
    END;
    p0 := newP0;
    p1 := newP1;
    FOR i := firstNewPage TO lastNewPage DO
      desc[i - p0].space := Space.Free;
    END;
    IF perfOn THEN
      PerfGrow(firstNewPage, lastNewPage - firstNewPage + 1);
    END;
    INC(allocatedPages, lastNewPage - firstNewPage + 1);
    RebuildFreelist();
  END GrowHeap;
</PRE>** SYNCHRONIZATION **

<P> Wait() waits on a condition, implemented compatibly with RTOS.LockHeap().
   Wait() will be called only from background threads.  The caller
   will be in a critical section.  

<P><PRE>VAR
  mutex     := NEW(MUTEX);
  condition := NEW(Thread.Condition);

PROCEDURE <A NAME="Wait"><procedure>Wait</procedure></A> () =
  BEGIN
    (* This procedure may not be called from inside the collector. *)
    LOCK mutex DO Thread.Wait(mutex, condition); END;
  END Wait;
</PRE> Broadcast() broadcasts a condition, implemented compatibly with
   RTOS.LockHeap().  Broadcast will not be called from inside the collector, so
   that references to the heap will succeed. 

<P><PRE>PROCEDURE <A NAME="Broadcast"><procedure>Broadcast</procedure></A> () =
  BEGIN
    Thread.Broadcast(condition);
  END Broadcast;
</PRE>** INITIALIZATION **

<P><PRE>CONST MaxAlignment  = 8;
CONST MaxAlignMask  = 2_0111;     (* bit mask to capture MaxAlignment *)
TYPE  MaxAlignRange = [0 .. MaxAlignment - 1];

VAR align: ARRAY MaxAlignRange, [1 .. MaxAlignment] OF CARDINAL;
</PRE><BLOCKQUOTE><EM> align[i,j] == RTMisc.Align (i, j) - i </EM></BLOCKQUOTE><PRE>

PROCEDURE <A NAME="Init"><procedure>Init</procedure></A> () =
  BEGIN
    weakTable := NEW(UNTRACED REF ARRAY OF WeakEntry, 0);

    (* initialize the alignment array *)
    FOR i := FIRST(align) TO LAST(align) DO
      FOR j := FIRST(align[0]) TO LAST(align[0]) DO
        align[i, j] := RTMisc.Upper(i, j) - i;
      END;
    END;
  END Init;

BEGIN
  IF RTParams.IsPresent(&quot;nogc&quot;) THEN disableCount := 1; END;
  IF RTParams.IsPresent(&quot;novm&quot;) THEN disableVMCount := 1; END;
  IF RTParams.IsPresent(&quot;noincremental&quot;) THEN incremental := FALSE; END;
  IF RTParams.IsPresent(&quot;nogenerational&quot;) THEN generational := FALSE; END;
  IF RTParams.IsPresent(&quot;paranoidgc&quot;) THEN InstallSanityCheck(); END;
  PerfStart();
END RTCollector.
</PRE>
</inModule>
<HR>
<A NAME="x1">interface RT0u is in:
</A><UL>
<LI><A HREF="../POSIX/RT0u.i3.html#0TOP0">runtime/src/POSIX/RT0u.i3</A>
<LI><A HREF="../WIN32/RT0u.i3.html#0TOP0">runtime/src/WIN32/RT0u.i3</A>
</UL>
<P>
<HR>
<A NAME="x2">interface RTMachine is in:
</A><UL>
<LI><A HREF="../AIX386/RTMachine.i3.html#0TOP0">runtime/src/AIX386/RTMachine.i3</A>
<LI><A HREF="../ALPHA_OSF/RTMachine.i3.html#0TOP0">runtime/src/ALPHA_OSF/RTMachine.i3</A>
<LI><A HREF="../AP3000/RTMachine.i3.html#0TOP0">runtime/src/AP3000/RTMachine.i3</A>
<LI><A HREF="../ARM/RTMachine.i3.html#0TOP0">runtime/src/ARM/RTMachine.i3</A>
<LI><A HREF="../DS3100/RTMachine.i3.html#0TOP0">runtime/src/DS3100/RTMachine.i3</A>
<LI><A HREF="../FreeBSD/RTMachine.i3.html#0TOP0">runtime/src/FreeBSD/RTMachine.i3</A>
<LI><A HREF="../FreeBSD2/RTMachine.i3.html#0TOP0">runtime/src/FreeBSD2/RTMachine.i3</A>
<LI><A HREF="../HP300/RTMachine.i3.html#0TOP0">runtime/src/HP300/RTMachine.i3</A>
<LI><A HREF="../HPPA/RTMachine.i3.html#0TOP0">runtime/src/HPPA/RTMachine.i3</A>
<LI><A HREF="../IBMR2/RTMachine.i3.html#0TOP0">runtime/src/IBMR2/RTMachine.i3</A>
<LI><A HREF="../IBMRT/RTMachine.i3.html#0TOP0">runtime/src/IBMRT/RTMachine.i3</A>
<LI><A HREF="../IRIX5/RTMachine.i3.html#0TOP0">runtime/src/IRIX5/RTMachine.i3</A>
<LI><A HREF="../LINUX/RTMachine.i3.html#0TOP0">runtime/src/LINUX/RTMachine.i3</A>
<LI><A HREF="../LINUXELF/RTMachine.i3.html#0TOP0">runtime/src/LINUXELF/RTMachine.i3</A>
<LI><A HREF="../NEXT/RTMachine.i3.html#0TOP0">runtime/src/NEXT/RTMachine.i3</A>
<LI><A HREF="../NT386/RTMachine.i3.html#0TOP0">runtime/src/NT386/RTMachine.i3</A>
<LI><A HREF="../OKI/RTMachine.i3.html#0TOP0">runtime/src/OKI/RTMachine.i3</A>
<LI><A HREF="../SEQUENT/RTMachine.i3.html#0TOP0">runtime/src/SEQUENT/RTMachine.i3</A>
<LI><A HREF="../SOLgnu/RTMachine.i3.html#0TOP0">runtime/src/SOLgnu/RTMachine.i3</A>
<LI><A HREF="../SOLsun/RTMachine.i3.html#0TOP0">runtime/src/SOLsun/RTMachine.i3</A>
<LI><A HREF="../SPARC/RTMachine.i3.html#0TOP0">runtime/src/SPARC/RTMachine.i3</A>
<LI><A HREF="../SUN3/RTMachine.i3.html#0TOP0">runtime/src/SUN3/RTMachine.i3</A>
<LI><A HREF="../SUN386/RTMachine.i3.html#0TOP0">runtime/src/SUN386/RTMachine.i3</A>
<LI><A HREF="../UMAX/RTMachine.i3.html#0TOP0">runtime/src/UMAX/RTMachine.i3</A>
<LI><A HREF="../VAX/RTMachine.i3.html#0TOP0">runtime/src/VAX/RTMachine.i3</A>
</UL>
<P>
<HR>
<A NAME="x3">interface RTPerfTool is in:
</A><UL>
<LI><A HREF="../POSIX/RTPerfTool.i3.html#0TOP0">runtime/src/POSIX/RTPerfTool.i3</A>
<LI><A HREF="../WIN32/RTPerfTool.i3.html#0TOP0">runtime/src/WIN32/RTPerfTool.i3</A>
</UL>
<P>
<HR>
<A NAME="x4">interface ThreadF is in:
</A><UL>
<LI><A HREF="../../../thread/src/NOOP/ThreadF.i3.html#0TOP0">thread/src/NOOP/ThreadF.i3</A>
<LI><A HREF="../../../thread/src/POSIX/ThreadF.i3.html#0TOP0">thread/src/POSIX/ThreadF.i3</A>
<LI><A HREF="../../../thread/src/WIN32/ThreadF.i3.html#0TOP0">thread/src/WIN32/ThreadF.i3</A>
</UL>
<P>
<PRE>























</PRE>
</BODY>
</HTML>
