<HTML>
<HEAD>
<TITLE>SRC Modula-3: runtime/src/common/RTAllocStats.m3</TITLE>
</HEAD>
<BODY>
<A NAME="0TOP0">
<H2>runtime/src/common/RTAllocStats.m3</H2></A><HR>
<inModule>
<PRE><A HREF="../../../COPYRIGHT.html">Copyright (C) 1994, Digital Equipment Corp.</A>
</PRE><BLOCKQUOTE><EM>                                                           </EM></BLOCKQUOTE><PRE>

UNSAFE MODULE <module><implements><A HREF="RTAllocStats.i3.html">RTAllocStats</A></implements></module>;

IMPORT <A HREF="RT0.i3.html">RT0</A>, <A HREF="#x1">RT0u</A>, <A HREF="RTMisc.i3.html">RTMisc</A>, <A HREF="RTType.i3.html">RTType</A>, <A HREF="../../../word/src/Word.i3.html">Word</A>, <A HREF="RTHeapRep.i3.html">RTHeapRep</A>, <A HREF="RTAllocator.i3.html">RTAllocator</A>;
IMPORT <A HREF="RTStack.i3.html">RTStack</A>, <A HREF="RTProcedure.i3.html">RTProcedure</A>, <A HREF="RTProcedureSRC.i3.html">RTProcedureSRC</A>, <A HREF="../../../text/src/Text.i3.html">Text</A>, <A HREF="../../../convert/src/Convert.i3.html">Convert</A>;
FROM <A HREF="RT0.i3.html">RT0</A> IMPORT Typecode;

CONST
  MaxSiteID = 255; (* == LAST (RT0.RefHeader.spare) *)
  MaxDepth  = 6;

TYPE
  Site = RECORD
    hash : Word.T;
    pcs  : ARRAY [0..MaxDepth] OF ADDRESS;
  END;

  SiteList = BRANDED REF ARRAY OF Site;

  TypeInfo = REF RECORD
    n_sites : INTEGER  := 0;
    sites   : SiteList := NIL;
  END;

  InfoList = BRANDED REF ARRAY OF TypeInfo;

VAR
  info : InfoList := NIL;

PROCEDURE <A NAME="EnableTrace"><procedure>EnableTrace</procedure></A> (tc: Typecode) =
  BEGIN
    IF NOT RTStack.Has_walker THEN RETURN END;
    IF NOT RTType.IsTraced (tc) THEN
      RTMisc.FatalError(NIL, 0,
                        &quot;RTAllocStats.EnableTrace(untraced type) was called&quot;);
    END;
    IF (info = NIL) THEN
      info := NEW (InfoList, RT0u.nTypes);
      RTAllocator.callback := NoteAllocation;
    END;
    info [tc] := NEW (TypeInfo, sites := NEW (SiteList, 4));
  END EnableTrace;

PROCEDURE <A NAME="NSites"><procedure>NSites</procedure></A> (tc: Typecode): INTEGER =
  VAR inf: TypeInfo := NIL;
  BEGIN
    IF (info = NIL) THEN RETURN -1; END;
    inf := info[tc];
    IF (inf = NIL) THEN RETURN -1; END;
    RETURN inf.n_sites;
  END NSites;

PROCEDURE <A NAME="GetSiteText"><procedure>GetSiteText</procedure></A> (tc: Typecode;  site, depth: CARDINAL): TEXT =
  VAR inf: TypeInfo := NIL;
  BEGIN
    IF (info = NIL) THEN RETURN NIL; END;
    inf := info[tc];

    IF (inf = NIL) THEN
      RETURN NIL;

    ELSIF (site = 0) THEN
      IF (depth &gt; 0)
        THEN RETURN NIL;
        ELSE RETURN &quot;OTHER SITES&quot;;
      END;

    ELSIF (site &gt; inf.n_sites) OR (depth &gt; MaxDepth) THEN
      RETURN NIL;

    ELSE
      RETURN PcToText (inf.sites[site-1].pcs[depth]);

    END;
  END GetSiteText;
</PRE>-------------------------------------------------------------- internal ---

<P><PRE>PROCEDURE <A NAME="NoteAllocation"><procedure>NoteAllocation</procedure></A> (ref: REFANY) =
  VAR inf: TypeInfo;  s: INTEGER;
  BEGIN
    IF (info = NIL) THEN RETURN END;
    inf := info [TYPECODE (ref)];
    IF (inf = NIL) THEN RETURN END;

    IF (inf.n_sites &gt; LAST (inf.sites^)) THEN ExpandSites (inf) END;

    WITH z = inf.sites [inf.n_sites] DO
      GetSite (z, 2);
      s := 0; WHILE (inf.sites[s].hash # z.hash) DO INC (s); END;
      IF (s &lt; MaxSiteID) THEN
        IF (s &gt;= inf.n_sites) THEN (* new site! *) INC (inf.n_sites); END;
        InsertSiteNum (ref, s+1);
      END;
    END;
  END NoteAllocation;

PROCEDURE <A NAME="ExpandSites"><procedure>ExpandSites</procedure></A> (inf: TypeInfo) =
  VAR n := NUMBER (inf.sites^);  new := NEW (SiteList, MIN (n+n, MaxSiteID+1));
  BEGIN
    SUBARRAY (new^, 0, n) := inf.sites^;
    inf.sites := new;
  END ExpandSites;

PROCEDURE <A NAME="InsertSiteNum"><procedure>InsertSiteNum</procedure></A> (ref: REFANY;  sitetag: INTEGER) =
  VAR
    addr := LOOPHOLE (ref, ADDRESS);
    hdr  := LOOPHOLE (addr - BYTESIZE(RT0.RefHeader), RTHeapRep.RefHeader);
  BEGIN
    hdr^.spare := sitetag;
  END InsertSiteNum;

PROCEDURE <A NAME="GetSite"><procedure>GetSite</procedure></A> (VAR(*OUT*) site: Site;  skip: CARDINAL) =
  VAR cur, prev: RTStack.Frame;
  BEGIN
    RTStack.CurrentFrame (cur);

    FOR i := 0 TO skip-1 DO
      RTStack.PreviousFrame (cur, prev);
      cur := prev;
    END;

    site.hash := 0;
    FOR i := 0 TO LAST (site.pcs) DO
      IF (cur.pc # NIL) AND (i &lt; siteDepth) THEN
        RTStack.PreviousFrame (cur, prev);  cur := prev;
        site.hash   := Word.Xor (site.hash, LOOPHOLE(cur.pc, Word.T));
        site.pcs[i] := cur.pc;
      ELSE
        site.pcs[i] := NIL;
      END;
    END;
  END GetSite;

PROCEDURE <A NAME="PcToText"><procedure>PcToText</procedure></A> (pc: ADDRESS): TEXT =
  &lt;*FATAL Convert.Failed*&gt;
  CONST NUL = '\000';
  VAR
    proc : RTProcedure.Proc;
    file : RTProcedureSRC.Name;
    name : RTProcedureSRC.Name;
    cp   : RT0.String;
    cur  : INTEGER := 0;
    len  : INTEGER;
    buf  : ARRAY [0..511] OF CHAR;
  BEGIN
    RTProcedureSRC.FromPC (pc, proc, file, name);
    IF (proc = NIL) THEN RETURN NIL END;

    cp := name;
    WHILE (cp # NIL) AND (cp^ # NUL) DO
      buf[cur] := cp^;  INC (cur);  INC (cp, ADRSIZE(cp^));
    END;

    IF (name # NIL) AND (pc # proc) THEN
      buf[cur] := ' ';  INC (cur);
      buf[cur] := '+';  INC (cur);
      buf[cur] := ' ';  INC (cur);
      len := Convert.FromUnsigned (
               SUBARRAY (buf, cur, NUMBER (buf) - cur),
               pc - proc, base := 16, prefix := TRUE);
      INC (cur, len);
    END;

    (* remove any path components from the file name *)
    cp := file;
    WHILE (cp # NIL) AND (cp^ # NUL) DO
      IF (cp^ = '/') OR (cp^ = '\134') THEN
        file := cp + ADRSIZE (cp^);
      END;
      INC (cp, ADRSIZE (cp^));
    END;

    IF (file # NIL) THEN
      buf[cur] := ' ';  INC (cur);
      buf[cur] := 'i';  INC (cur);
      buf[cur] := 'n';  INC (cur);
      buf[cur] := ' ';  INC (cur);
    END;

    cp := file;
    WHILE (cp # NIL) AND (cp^ # NUL) DO
      buf[cur] := cp^;  INC (cur);  INC (cp, ADRSIZE(cp^));
    END;

    RETURN Text.FromChars (SUBARRAY (buf, 0, cur));
  END PcToText;

BEGIN
END RTAllocStats.
</PRE>
</inModule>
<HR>
<A NAME="x1">interface RT0u is in:
</A><UL>
<LI><A HREF="../POSIX/RT0u.i3.html#0TOP0">runtime/src/POSIX/RT0u.i3</A>
<LI><A HREF="../WIN32/RT0u.i3.html#0TOP0">runtime/src/WIN32/RT0u.i3</A>
</UL>
<P>
<PRE>























</PRE>
</BODY>
</HTML>
