<HTML>
<HEAD>
<TITLE>SRC Modula-3: ui/src/winvbt/WinContext.m3</TITLE>
</HEAD>
<BODY>
<A NAME="0TOP0">
<H2>ui/src/winvbt/WinContext.m3</H2></A><HR>
<inModule>
<PRE><A HREF="../../../COPYRIGHT.html">Copyright (C) 1994, Digital Equipment Corp.</A>
</PRE><BLOCKQUOTE><EM> Digital Internal Use Only                                                 </EM></BLOCKQUOTE><PRE>
</PRE>                                                                           
       Created on Wed Feb 15 16:03:36 PST 1995 by najork                   

<P><PRE>UNSAFE MODULE <module><implements><A HREF="WinContext.i3.html">WinContext</A></implements></module>;

IMPORT <A HREF="../../../C/src/Common/Ctypes.i3.html">Ctypes</A>, <A HREF="../vbt/PaintPrivate.i3.html">PaintPrivate</A>, <A HREF="../../../geometry/src/Point.i3.html">Point</A>, <A HREF="../../../geometry/src/Rect.i3.html">Rect</A>, <A HREF="../vbt/VBT.i3.html">VBT</A>, <A HREF="../vbt/VBTRep.i3.html">VBTRep</A>, <A HREF="../../../win32/src/WinDef.i3.html">WinDef</A>, <A HREF="../../../win32/src/WinGDI.i3.html">WinGDI</A>,
       <A HREF="WinScrnPixmap.i3.html">WinScrnPixmap</A>, <A HREF="WinScreenType.i3.html">WinScreenType</A>, <A HREF="WinScreenTypePrivate.i3.html">WinScreenTypePrivate</A>;

CONST
  True = 1;
</PRE> Alternatively, I could use <CODE>SaveDC</CODE> and <CODE>RestoreDC</CODE> to save and restore
   the device context. I'm not sure which is more efficient -- does <CODE>SaveDC</CODE>
   perform heap allocations every time? 

<P>
<P>
<P> PushTint is functioanlly equivalent to XGC.ResolveTintGC, except that 
   it does not deal with the <CODE>plane_mask</CODE>. 

<P>
<P><PRE>PROCEDURE <A NAME="PushTint"><procedure>PushTint</procedure></A> (hdc: WinDef.HDC;
                    st : WinScreenType.T;
                    op : PaintPrivate.PaintOp): T =
  VAR
    ctxt : T;
    brush: WinDef.HBRUSH;
  BEGIN
    ctxt.hdc := hdc;

    IF op &gt;= 0 AND st.optable # NIL AND op &lt; NUMBER (st.optable^) THEN
      WITH tbl = st.optable[op] DO
        ctxt.rop2 := WinGDI.SetROP2 (hdc, tbl.rop2);
        &lt;* ASSERT ctxt.rop2 # 0 *&gt;
        brush := WinGDI.CreateSolidBrush (tbl.fop.col);
        &lt;* ASSERT brush # NIL *&gt;
        ctxt.brush := WinGDI.SelectObject (hdc, brush);
        &lt;* ASSERT ctxt.brush # NIL *&gt;
      END;
    ELSE
      ctxt.rop2 := WinGDI.SetROP2 (hdc, WinGDI.R2_NOP);
      &lt;* ASSERT ctxt.rop2 # 0 *&gt;
    END;
    RETURN ctxt;
  END PushTint;
</PRE> Unresolved issues:
     o The origin of the pattern lies somewhere different than in the X world.
     o I don't think I handle complex PaintOps well


<P><PRE>PROCEDURE <A NAME="PushTexture"><procedure>PushTexture</procedure></A> (hdc  : WinDef.HDC;
                       st   : WinScreenType.T;
                       op   : PaintPrivate.PaintOp;
                       pm   : PaintPrivate.Pixmap;
                       delta: Point.T): T =
  VAR
    ctxt : T;
    pst  : WinScreenType.T;
    apm  : PaintPrivate.Pixmap := pm;
    color: WinDef.COLORREF;
    brush: WinDef.HBRUSH;
    oldOrg: WinDef.POINT;
    status: WinDef.BOOL;
  BEGIN
    ctxt.hdc := hdc;

    IF pm = WinScrnPixmap.SolidPixmap THEN
      RETURN PushTint (hdc, st, op);
    END;
    IF pm &lt; 0 THEN
      pm := WinScrnPixmap.SolidPixmap - pm;
      pst := st.bits;
    ELSE
      pst := st;
    END;
    IF delta # Point.Origin THEN
      WITH pmb = WinScrnPixmap.PixmapDomain (st, apm) DO
        IF NOT Rect.IsEmpty (pmb) THEN
          delta := Rect.Mod (delta, pmb);
        END;
      END;
    END;
    IF op &gt;= 0 AND st.optable # NIL AND op &lt; NUMBER(st.optable^) AND
       pst.pmtable # NIL AND pm &lt; NUMBER (pst.pmtable^) THEN
      WITH tbl = st.optable[op] DO
        ctxt.rop2 := WinGDI.SetROP2 (hdc, tbl.rop2);
        &lt;* ASSERT ctxt.rop2 # 0 *&gt;

        brush := WinGDI.CreatePatternBrush (pst.pmtable[pm].hbmp);
        &lt;* ASSERT brush # NIL *&gt;
        status := WinGDI.SetBrushOrgEx(hdc,
                                       delta.h + pst.pmtable[pm].domain.west,
                                       delta.v + pst.pmtable[pm].domain.north,
                                       ADR(oldOrg));
        &lt;* ASSERT status = True *&gt;

        ctxt.brush := WinGDI.SelectObject (hdc, brush);
        &lt;* ASSERT ctxt.brush # NIL *&gt;

        (* In Windows, '0' pixels of the bitmap in the pattern brush are
           drawn in the current text color, so the text color should be
           &quot;tbl.bop.col&quot;. '1' pixels are drawn in the current background color,
           so this color should be &quot;tbl.fop.col&quot;. Counterintuive? Well, after
           all, this is Windows! *)
        color := WinGDI.SetTextColor (hdc, tbl.bop.col);
        &lt;* ASSERT color # WinGDI.CLR_INVALID *&gt;
        color := WinGDI.SetBkColor (hdc, tbl.fop.col);
        &lt;* ASSERT color # WinGDI.CLR_INVALID *&gt;
      END;
    ELSE
      ctxt.rop2 := WinGDI.SetROP2 (hdc, WinGDI.R2_NOP);
      &lt;* ASSERT ctxt.rop2 # 0 *&gt;
    END;
    RETURN ctxt;
  END PushTexture;

PROCEDURE <A NAME="PushFill"><procedure>PushFill</procedure></A> (hdc  : WinDef.HDC;
                    st   : WinScreenType.T;
                    op   : PaintPrivate.PaintOp;
                    pm   : PaintPrivate.Pixmap;
                    delta: Point.T;
                    wind : VBT.WindingCondition): T =
  CONST
    FillStyle = ARRAY VBT.WindingCondition OF INTEGER {WinGDI.ALTERNATE,
                                                       WinGDI.WINDING};
  VAR
    ctxt  : T;
    pen   : WinDef.HPEN;
    brush : WinDef.HBRUSH;
    status: Ctypes.int;
    pst   : WinScreenType.T     := st;  (* pixmap screen type *)
    apm   : PaintPrivate.Pixmap := pm;  (* backup of original &quot;pm&quot; argument *)
    color : WinDef.COLORREF;
  BEGIN
    ctxt.hdc := hdc;

    IF pm &lt; 0 THEN
      pm := WinScrnPixmap.SolidPixmap - pm;
      pst := st.bits;
    END;
    IF delta # Point.Origin THEN
      WITH pmb = WinScrnPixmap.PixmapDomain(st, apm) DO
        IF NOT Rect.IsEmpty(pmb) THEN
          delta := Rect.Mod (delta, pmb);
        END;
      END;
    END;
    (* Note: we could also have a &quot;fast path&quot; for empty pixmaps *)

    IF op &gt;= 0 AND st.optable # NIL AND op &lt; NUMBER(st.optable^)
       AND pst.pmtable # NIL AND pm &lt; NUMBER (pst.pmtable^) THEN

      WITH tbl = st.optable[op] DO
        ctxt.rop2 := WinGDI.SetROP2 (hdc, tbl.rop2);
        &lt;* ASSERT ctxt.rop2 # 0 *&gt;

        status := WinGDI.SetPolyFillMode (hdc, FillStyle[wind]);
        &lt;* ASSERT status # 0 *&gt;
        (* Note: &quot;Pop&quot; does not try to reestablish the previous fill style.
           So, there is no need to save it in &quot;ctxt&quot;. *)

        pen := WinGDI.GetStockObject (WinGDI.NULL_PEN);
        ctxt.pen := WinGDI.SelectObject (hdc, pen);
        &lt;* ASSERT ctxt.pen # NIL *&gt;

        IF apm = WinScrnPixmap.SolidPixmap THEN
          brush := WinGDI.CreateSolidBrush (tbl.fop.col);
          &lt;* ASSERT brush # NIL *&gt;
          ctxt.brush := WinGDI.SelectObject (hdc, brush);
          &lt;* ASSERT ctxt.brush # NIL *&gt;
        ELSE

          brush := WinGDI.CreatePatternBrush (pst.pmtable[pm].hbmp);
          &lt;* ASSERT brush # NIL *&gt;
          ctxt.brush := WinGDI.SelectObject (hdc, brush);
          &lt;* ASSERT ctxt.brush # NIL *&gt;

          (* In Windows, '0' pixels of the bitmap in the pattern brush are
             drawn in the current text color, so the text color should be
             &quot;tbl.bop.col&quot;. '1' pixels are drawn in the current background
             color, so this color should be &quot;tbl.fop.col&quot;. Counterintuive?
             Well, after all, this is Windows! *)
          color := WinGDI.SetTextColor (hdc, tbl.bop.col);
          &lt;* ASSERT color # WinGDI.CLR_INVALID *&gt;
          color := WinGDI.SetBkColor (hdc, tbl.fop.col);
          &lt;* ASSERT color # WinGDI.CLR_INVALID *&gt;
        END;
      END;
    ELSE
      ctxt.rop2 := WinGDI.SetROP2 (hdc, WinGDI.R2_NOP);
      &lt;* ASSERT ctxt.rop2 # 0 *&gt;
    END;
    RETURN ctxt;
  END PushFill;

PROCEDURE <A NAME="PushStroke"><procedure>PushStroke</procedure></A> (hdc  : WinDef.HDC;
                      st   : WinScreenType.T;
                      op   : PaintPrivate.PaintOp;
                      pm   : PaintPrivate.Pixmap;
                      delta: Point.T;
                      width: CARDINAL;
                      end  : VBT.EndStyle;
                      join : VBT.JoinStyle): T =
  CONST
    EndStyle = ARRAY VBT.EndStyle OF INTEGER {WinGDI.PS_ENDCAP_ROUND,
                                              WinGDI.PS_ENDCAP_FLAT,
                                              WinGDI.PS_ENDCAP_SQUARE};
    JoinStyle = ARRAY VBT.JoinStyle OF INTEGER {WinGDI.PS_JOIN_ROUND,
                                                WinGDI.PS_JOIN_BEVEL,
                                                WinGDI.PS_JOIN_MITER};
  VAR
    ctxt    : T;
    pen     : WinDef.HPEN;
    style   : WinDef.DWORD;
    logbrush: WinGDI.LOGBRUSH;
    pst     : WinScreenType.T     := st; (* pixmap screen type *)
    apm     : PaintPrivate.Pixmap := pm; (* backup of original &quot;pm&quot; argument *)
    color   : WinDef.COLORREF;
  BEGIN
    ctxt.hdc := hdc;

    IF pm &lt; 0 THEN
      pm := WinScrnPixmap.SolidPixmap - pm;
      pst := st.bits;
    END;
    IF delta # Point.Origin THEN
      WITH pmb = WinScrnPixmap.PixmapDomain(st, apm) DO
        IF NOT Rect.IsEmpty(pmb) THEN
          delta := Rect.Mod (delta, pmb);
        END;
      END;
    END;

    (*
     * The main omission is that I don't deal with pixmaps!
     *
     * Refer back to XGC.ResolveStrokeGC to see just how much functionality
     * is missing!
     *)

    IF op &gt;= 0 AND st.optable # NIL AND op &lt; NUMBER (st.optable^)
       AND pst.pmtable # NIL AND pm &lt; NUMBER(pst.pmtable^) THEN
      WITH tbl = st.optable[op] DO

        ctxt.rop2 := WinGDI.SetROP2 (hdc, tbl.rop2);
        &lt;* ASSERT ctxt.rop2 # 0 *&gt;

        style := WinGDI.PS_GEOMETRIC + WinGDI.PS_SOLID +
                     EndStyle[end] + JoinStyle[join];

        IF apm = WinScrnPixmap.SolidPixmap THEN
          logbrush.lbStyle := WinGDI.BS_SOLID;
          logbrush.lbColor := tbl.fop.col;
          logbrush.lbHatch := WinGDI.HS_HORIZONTAL;
            (* ignored if style is solid *)
        ELSE
          logbrush.lbStyle := WinGDI.BS_PATTERN;
          logbrush.lbHatch := LOOPHOLE (pst.pmtable[pm].hbmp, WinDef.LONG);
          (*
           * From the documentation, it is not clear how to select the
           * background and foreground colors for the bitmap.  An experiment
           * showed that (at least on NT 3.1) &quot;SetTextColor&quot; selected the
           * background color, and &quot;SetBkColor&quot; selected the foreground color.
           *)
          color := WinGDI.SetTextColor (hdc, tbl.bop.col);
          &lt;* ASSERT color # WinGDI.CLR_INVALID *&gt;
          color := WinGDI.SetBkColor (hdc, tbl.fop.col);
          &lt;* ASSERT color # WinGDI.CLR_INVALID *&gt;
        END;
        pen := WinGDI.ExtCreatePen (style, width, ADR(logbrush), 0, NIL);
        &lt;* ASSERT pen # NIL *&gt;
        ctxt.pen := WinGDI.SelectObject (hdc, pen);
        &lt;* ASSERT ctxt.pen # NIL *&gt;
      END;
    ELSE
      ctxt.rop2 := WinGDI.SetROP2 (hdc, WinGDI.R2_NOP);
      &lt;* ASSERT ctxt.rop2 # 0 *&gt;
    END;

    RETURN ctxt;
  END PushStroke;

PROCEDURE <A NAME="Pop"><procedure>Pop</procedure></A> (READONLY ctxt: T) =
  VAR
    rop2 : Ctypes.int;
    pen  : WinDef.HPEN;
    brush: WinDef.HBRUSH;
    bool : WinDef.BOOL;
  BEGIN
    (* If necessary, reset the binary raster operation mode. *)
    IF ctxt.rop2 # 0 THEN
      rop2 := WinGDI.SetROP2 (ctxt.hdc, ctxt.rop2);
      &lt;* ASSERT rop2 # 0 *&gt;
    END;

    (* If necessary, reset the pen and free up the pen handle *)
    IF ctxt.pen # NIL THEN
      pen := WinGDI.SelectObject (ctxt.hdc, ctxt.pen);
      &lt;* ASSERT pen # NIL *&gt;
      bool := WinGDI.DeleteObject (pen);
      &lt;* ASSERT bool = True *&gt;
    END;

    (* If necessary, reset the brush and free up the brush handle *)
    IF ctxt.brush # NIL THEN
      brush := WinGDI.SelectObject (ctxt.hdc, ctxt.brush);
      &lt;* ASSERT brush # NIL *&gt;
      bool := WinGDI.DeleteObject (brush);
      &lt;* ASSERT bool = True *&gt;
    END;
  END Pop;

BEGIN
END WinContext.
</PRE>
</inModule>
<PRE>























</PRE>
</BODY>
</HTML>
